FUNCTION "Gestion_Melangeuse" : Void
{ S7_Optimized_Access := 'TRUE' }
VERSION : 0.1
   VAR_INPUT 
      Fg_Base : Bool;
      Raz_Pt_Zs : "S_Suivi_Dosage";
   END_VAR

   VAR_IN_OUT 
      Melangeuse : "S_Melangeuse";
   END_VAR

   VAR_TEMP 
      Ret_Test_Lot_Att_Tache : SInt;
      Ret_Maj_Niv : SInt;
      Ret_Dem_Mobile : SInt;
      Ret_RD_SYS_T : Int;
      Tps_Passe : Time;
      Raz_Compo : "S_Com_Fab";
      Ret_Arr_Imm_Mobile : SInt;
      Ret_Vid_Mobile : SInt;
      Tps_Act : Date_And_Time;
      i : USInt;
      k : SInt;
   END_VAR

   VAR CONSTANT 
      Etp_Repos : SInt := 0;
      Etp_Att_Cond : SInt := 1;
      Etp_Att_Amont : SInt := 2;
      Etp_Debut_Remp : SInt := 3;
      Etp_Att_Fin_Amont : SInt := 4;
      Etp_Premelange : SInt := 5;
      Etp_Inj_Liquide : SInt := 6;
      Etp_Melange : SInt := 7;
      Etp_Att_Aval_Pret : SInt := 8;
      Etp_Deb_Vid : SInt := 9;
      Etp_Att_Prd : SInt := 10;
      Etp_Tempo_Vid_Melang : SInt := 11;
      Etp_Att_Fin_Prd : SInt := 12;
      Etp_Ferm_Melang : SInt := 13;
      Etp_Fin_Lot : SInt := 14;
      Etp_Att_Acq : SInt := 15;
      Pos_Ouverture : SInt := 1;
      Pos_Fermeture : SInt := 0;
   END_VAR


BEGIN
	// //============= Affichage Compo Supervision =============//
	 "FC_Affich_Liquides_Superv"(#Melangeuse);
	// //============= Affichage Compo Supervision =============//
	//============= Affichage Etiquette =============//
	 "FC_Affich_Etiquette"(Couleur := #Melangeuse.Pt_Zs._SuiviCommun._Couleur,
	                       Cel_Dest := #Melangeuse.Pt_Zs._Cel_Dest,
	                       CdMat := #Melangeuse.Pt_Zs._SuiviCommun._CdMat,
	                       NoLot := #Melangeuse.Pt_Zs._SuiviCommun._NoLot,
	                       QteLot := #Melangeuse.Pt_Zs._SuiviCommun._QteLot,
	                       Desc_Etp := #Melangeuse.CommunDos.Module_Commun._DescEtp,
	                       Etiquette := #Melangeuse.CommunDos);
	//============= Affichage Etiquette =============//
	#Ret_RD_SYS_T := RD_SYS_T(#Tps_Act);
	CASE #Melangeuse.CommunDos.Module_Commun._Etp OF
	    #Etp_Repos:
	        REGION Preliminaire Repos
	            IF #Melangeuse.CommunDos.Module_Commun._Etp <> #Melangeuse.CommunDos.Module_Commun._EtpMemo THEN
	                #Melangeuse.CommunDos.Module_Commun._DescEtp := 'REPOS';
	                #Melangeuse.Pt_Zs := #Raz_Pt_Zs;
	                //============= Affichage Compo Supervision =============//
	                FOR #i := 0 TO "Nbr_Max_Compo" DO
	                    #Melangeuse.Lst_Compos[#i] := #Raz_Compo;
	                END_FOR;
	                //============= Affichage Compo Supervision =============//
	                #Melangeuse.CommunDos.Module_Commun._EtpMemo := #Melangeuse.CommunDos.Module_Commun._Etp;
	            END_IF;
	        END_REGION Preliminaire Repos
	        REGION Cyclique Repos
	            #Ret_Test_Lot_Att_Tache := "Test_Lot_Att_Tache"(Num_Tache := #Melangeuse.CommunDos.Module_Commun._Tache,
	                                                            Num_Zone => #Melangeuse.CommunDos.Module_Commun._NumZone);
	            IF #Ret_Test_Lot_Att_Tache = 1 THEN
	                #Melangeuse.Pt_Zs := "DB_Data_Dosage"."THIS"[#Melangeuse.CommunDos.Module_Commun._NumZone];
	                // Recuperation des composants a doser dans ce lieux
	                FOR #i := 0 TO "Nbr_Max_Compo" DO
	                    IF #Melangeuse.Pt_Zs._Lst_Compos[#i].Incorpo = "Nom_Mobile"(#Melangeuse.Mob_Melang) THEN
	                        #Melangeuse.Lst_Compos[#k] := #Melangeuse.Pt_Zs._Lst_Compos[#i];
	                        #k := #k + 1;
	                    END_IF; 
	                END_FOR;
	                #Ret_Maj_Niv := "Maj_Niv"(Num_Zone := #Melangeuse.CommunDos.Module_Commun._NumZone,
	                                          Num_Tache := #Melangeuse.CommunDos.Module_Commun._Tache,
	                                          Niveau := "NIV_MLG_DEBUT_LOT");
	                #Melangeuse.CommunDos.Module_Commun._Etp := #Etp_Att_Cond;
	            END_IF;
	        END_REGION Cyclique Repos
	    #Etp_Att_Cond:
	        REGION Preliminaire Att Cond
	            IF #Melangeuse.CommunDos.Module_Commun._Etp <> #Melangeuse.CommunDos.Module_Commun._EtpMemo THEN
	                #Melangeuse.CommunDos.Module_Commun._DescEtp := 'ATT COND';
	                #Melangeuse.CommunDos.Module_Commun._EtpMemo := #Melangeuse.CommunDos.Module_Commun._Etp;
	            END_IF;
	        END_REGION  Preliminaire Att Cond 
	        REGION Cyclique Att Cond
	            IF "Valid_Index"(#Melangeuse.Mob_Trappe) THEN
	                "FC_Posit_Mob"(Index_Mob := #Melangeuse.Mob_Trappe,Position := #Pos_Fermeture);
	                #Ret_Maj_Niv := "Maj_Niv"(Num_Zone := #Melangeuse.CommunDos.Module_Commun._NumZone,
	                                          Num_Tache := #Melangeuse.CommunDos.Module_Commun._Tache,
	                                          Niveau := "NIV_MLG_PRETE");
	            END_IF;
	            IF "Valid_Index"(#Melangeuse.Mob_Trappe) THEN
	                IF NOT "Test_Etat_Mobile"("Index_Mobile":=#Melangeuse.Mob_Trappe ,Etat:="Pret") THEN
	                    RETURN;
	                END_IF;
	            END_IF;
	            IF "Valid_Index"(#Melangeuse.Mob_Verrou) THEN
	                "FC_Posit_Mob"(Index_Mob := #Melangeuse.Mob_Verrou,
	                               Position := #Pos_Fermeture);
	            END_IF;
	            #Melangeuse.CommunDos.Module_Commun._Etp := #Etp_Att_Amont;
	        END_REGION Cyclique Att Cond
	    #Etp_Att_Amont:
	        REGION Preliminaire Att Amont
	            IF #Melangeuse.CommunDos.Module_Commun._Etp  <> #Melangeuse.CommunDos.Module_Commun._EtpMemo THEN
	                #Melangeuse.CommunDos.Module_Commun._DescEtp := 'ATT AMONT';
	                #Melangeuse.CommunDos.Module_Commun._EtpMemo := #Melangeuse.CommunDos.Module_Commun._Etp;
	            END_IF;
	        END_REGION Preliminaire Att Amont
	        REGION Cyclique Att Amont
	            IF NOT "FC_Test_Niv_AmontAval"(Num_Zone:=#Melangeuse.CommunDos.Module_Commun._NumZone, Lst_Taches:=#Melangeuse.Lst_Taches_Att_Aut_Demar) THEN
	                RETURN;
	            END_IF;
	            IF "Valid_Index"(#Melangeuse.Mob_Melang) THEN
	                #Melangeuse.SAV_Melang := #Melangeuse.Pt_Zs._ParamMelang._Vit_Mlg;
	                #Ret_Dem_Mobile := "Dem_Mobile"(#Melangeuse.Mob_Melang);
	                #Ret_Maj_Niv := "Maj_Niv"(Num_Zone := #Melangeuse.CommunDos.Module_Commun._NumZone,
	                                          Num_Tache := #Melangeuse.CommunDos.Module_Commun._Tache,
	                                          Niveau := "NIV_MLG_MARCHE");
	            END_IF;
	           #Melangeuse.CommunDos.Module_Commun._Etp := #Etp_Debut_Remp;
	        END_REGION Cyclique Att Amont
	    #Etp_Debut_Remp:
	        REGION Preliminaire Debut Remp
	            IF #Melangeuse.CommunDos.Module_Commun._Etp <> #Melangeuse.CommunDos.Module_Commun._EtpMemo THEN
	                #Melangeuse.CommunDos.Module_Commun._DescEtp := 'DEBUT REMP';
	                #Melangeuse.CommunDos.Module_Commun._EtpMemo := #Melangeuse.CommunDos.Module_Commun._Etp;
	            END_IF;
	        END_REGION Preliminaire Debut Remp
	        REGION Cyclique Debut Remp
	            IF NOT "FC_Test_Niv_AmontAval"(Num_Zone := #Melangeuse.CommunDos.Module_Commun._NumZone, Lst_Taches := #Melangeuse.Lst_Taches_Att_Debut_Remp) THEN
	                RETURN;
	            END_IF;
	            #Ret_Maj_Niv := "Maj_Niv"(Num_Zone := #Melangeuse.CommunDos.Module_Commun._NumZone,
	                                      Num_Tache := #Melangeuse.CommunDos.Module_Commun._Tache,
	                                      Niveau := "NIV_MLG_DEBUT_REMP");
	            #Melangeuse.CommunDos.Module_Commun._Etp := #Etp_Att_Fin_Amont;
	        END_REGION Cyclique Debut Remp
	    #Etp_Att_Fin_Amont:
	        REGION Preliminaire Att Fin Amont
	            IF #Melangeuse.CommunDos.Module_Commun._Etp <> #Melangeuse.CommunDos.Module_Commun._EtpMemo THEN
	                #Melangeuse.CommunDos.Module_Commun._DescEtp := 'ATT FIN AMONT';
	                #Melangeuse.CommunDos.Module_Commun._EtpMemo := #Melangeuse.CommunDos.Module_Commun._Etp;
	            END_IF;
	        END_REGION Preliminaire Att Fin Amont
	        REGION Cyclique Att fin  Amont
	            IF NOT "FC_Test_Niv_AmontAval"(Num_Zone := #Melangeuse.CommunDos.Module_Commun._NumZone, Lst_Taches := #Melangeuse.Lst_Taches_Att_Fin_Amont) THEN
	                RETURN;
	            END_IF;
	            #Ret_Maj_Niv := "Maj_Niv"(Num_Zone := #Melangeuse.CommunDos.Module_Commun._NumZone,
	                                      Num_Tache := #Melangeuse.CommunDos.Module_Commun._Tache,
	                                      Niveau := "NIV_MLG_FIN_REMP");
	            // Lire la tempo maxi du pt_zs et l'affecter au tpsmax de la melangeuse
	            // Lancement controle
	            // F_Tps_MLG_Max=1;
	            #Melangeuse.CommunDos.Module_Commun._Etp := #Etp_Premelange;
	        END_REGION  Cyclique Att fin  Amont
	    #Etp_Premelange:
	        REGION Preliminaire Premelange
	            IF #Melangeuse.CommunDos.Module_Commun._Etp <> #Melangeuse.CommunDos.Module_Commun._EtpMemo THEN
	                #Melangeuse.CommunDos.Module_Commun._DescEtp := 'PREMELANGE';
	                #Ret_RD_SYS_T := RD_SYS_T(#Melangeuse.CommunDos.Module_Commun._TpsPicke);
	                #Melangeuse.CommunDos.Module_Commun._EtpMemo := #Melangeuse.CommunDos.Module_Commun._Etp;
	            END_IF;
	        END_REGION Preliminaire Premelange
	        REGION Cyclique Premelange
	            IF NOT "Tempo"(Tps_Picke := #Melangeuse.CommunDos.Module_Commun._TpsPicke, Duree := #Melangeuse.Pt_Zs._ParamMelang._Tps_Pre_Mlg, Anticip := FALSE, Tps_Passe => #Tps_Passe) THEN
	                // Pour Affichage
	                #Melangeuse.Tps_Passe := TIME_TO_UDINT(#Tps_Passe)/1000;
	                #Melangeuse.Tps_Total := TIME_TO_UDINT(#Melangeuse.Pt_Zs._ParamMelang._Tps_Pre_Mlg)/1000;
	                RETURN;
	            END_IF;
	            #Melangeuse.CommunDos.Module_Commun._Etp := #Etp_Inj_Liquide;
	        END_REGION Cyclique Premelange
	    #Etp_Inj_Liquide:
	        REGION Preliminaire Inj Liquide
	            IF #Melangeuse.CommunDos.Module_Commun._Etp <> #Melangeuse.CommunDos.Module_Commun._EtpMemo THEN
	                #Melangeuse.CommunDos.Module_Commun._DescEtp := 'INJ. LIQ.';
	                #Melangeuse.CommunDos.Module_Commun._EtpPrec := #Melangeuse.CommunDos.Module_Commun._Etp;
	                //=== Traitement cas retour de defaut ===//
	                IF #Melangeuse.Dial_Def.Reponse_Def <> '' THEN
	                    #Melangeuse.CommunDos.Module_Commun._EtpMemo := #Melangeuse.CommunDos.Module_Commun._Etp;
	                    CASE #Melangeuse.Dial_Def.Num_Def_Etape OF
	                        1:
	                            GOTO Inj_Liquide_DEF1;
	                    END_CASE;
	                END_IF;
	                //=== Traitement cas retour de defaut ===//
	                #Ret_Maj_Niv := "Maj_Niv"(Num_Zone := #Melangeuse.CommunDos.Module_Commun._NumZone,
	                                          Num_Tache := #Melangeuse.CommunDos.Module_Commun._Tache,
	                                          Niveau := "NIV_MLG_FIN_PREMELANG");
	                #Ret_RD_SYS_T := RD_SYS_T(#Melangeuse.CommunDos.Module_Commun._TpsPicke);
	                #Melangeuse.CommunDos.Module_Commun._EtpMemo := #Melangeuse.CommunDos.Module_Commun._Etp;
	            END_IF;
	        END_REGION Preliminaire Inj Liquide
	        REGION Cyclique Inj Liquide   
	            IF "Tempo"(Tps_Picke := #Melangeuse.CommunDos.Module_Commun._TpsPicke, Duree := #Melangeuse.Pt_Zs._ParamMelang._Tps_Inj_Max, Anticip := FALSE, Tps_Passe => #Tps_Passe) THEN
	                // DIALOGUE DEFAUT
	                #Melangeuse.Dial_Def.Num_Def_Etape := 1;
	                #Melangeuse.Dial_Def.Index_Defaut := "Dialog_Defaut"(Fg_Base:=#Fg_Base, Msg_Defaut := 'Defaut Att Fin Injection Liquide',
	                                                                     Type_Defaut := 0,
	                                                                     Automatisme := #Melangeuse.Pt_Zs._SuiviCommun._CdProc,
	                                                                     Num_Lot := #Melangeuse.Pt_Zs._SuiviCommun._NoLot,
	                                                                     Origine_Defaut := '',
	                                                                     Libelle_Origine := #Melangeuse.CommunDos.Module_Commun._NomTache,
	                                                                     Acq1 := 'Relance',
	                                                                     Acq2 := 'Abandon',
	                                                                     Acq3 := '',
	                                                                     Acq4 := '',
	                                                                     Acq5 := '',
	                                                                     Acq6 := '',
	                                                                     Acq7 := '',
	                                                                     Acq8 := '',
	                                                                     Acq_Choisi := -1,
	                                                                     Date_Arrivee := #Tps_Act);
	                #Melangeuse.CommunDos.Module_Commun._Etp := #Etp_Att_Acq;
	                RETURN;
	            END_IF;
	            // Pour Affichage
	            #Melangeuse.Tps_Passe := TIME_TO_UDINT(#Tps_Passe)/1000;
	            #Melangeuse.Tps_Total := TIME_TO_UDINT(#Melangeuse.Pt_Zs._ParamMelang._Tps_Inj_Max)/1000;
	        Inj_Liquide_DEF1:
	            //========================== Traitement des reponses defaut ==========================//
	            IF (#Melangeuse.Dial_Def.Reponse_Def = 'Relance') THEN
	                // Reinitialisation de la tempo
	                #Ret_RD_SYS_T := RD_SYS_T(#Melangeuse.CommunDos.Module_Commun._TpsPicke);
	                // RAZ message defaut
	                #Melangeuse.Dial_Def.Reponse_Def := '';
	                RETURN;
	            END_IF;
	            IF (#Melangeuse.Dial_Def.Reponse_Def = 'Abandon') THEN
	                // RAZ message defaut
	                #Melangeuse.Dial_Def.Reponse_Def := '';
	                #Melangeuse.CommunDos.Module_Commun._Etp := #Etp_Melange;
	                RETURN;
	            END_IF;
	            //========================== Traitement des reponses defaut ==========================//
	            IF NOT "FC_Test_Niv_AmontAval"(Num_Zone := #Melangeuse.CommunDos.Module_Commun._NumZone, Lst_Taches := #Melangeuse.Lst_Taches_Att_Fin_Liq) THEN
	                RETURN;
	            END_IF;
	            #Melangeuse.CommunDos.Module_Commun._Etp := #Etp_Melange;
	        END_REGION Cyclique Inj Liquide
	    #Etp_Melange:
	        REGION Preliminaire Melange
	            IF #Melangeuse.CommunDos.Module_Commun._Etp <> #Melangeuse.CommunDos.Module_Commun._EtpMemo THEN
	                #Melangeuse.CommunDos.Module_Commun._DescEtp := 'MELANGE';
	                "Maj_Qte_Tache"(Num_Tache := #Melangeuse.CommunDos.Module_Commun._Tache,
	                                Num_Zone := #Melangeuse.CommunDos.Module_Commun._NumZone,
	                                Qte := "Calc_Qte_Tache"(Num_Zone := #Melangeuse.CommunDos.Module_Commun._NumZone, Lst_Amont := #Melangeuse.Lst_Taches_Amont_Qte));
	                #Ret_Maj_Niv := "Maj_Niv"(Num_Zone := #Melangeuse.CommunDos.Module_Commun._NumZone,
	                                          Num_Tache := #Melangeuse.CommunDos.Module_Commun._Tache,
	                                          Niveau := "NIV_MLG_FIN_INJ_LIQ");
	                #Ret_RD_SYS_T := RD_SYS_T(#Melangeuse.CommunDos.Module_Commun._TpsPicke);
	                #Melangeuse.CommunDos.Module_Commun._EtpMemo := #Melangeuse.CommunDos.Module_Commun._Etp;
	            END_IF;
	        END_REGION Preliminaire Melange
	        REGION Cyclique Melange            
	            IF NOT "Tempo"(Tps_Picke := #Melangeuse.CommunDos.Module_Commun._TpsPicke, Duree := #Melangeuse.Pt_Zs._ParamMelang._Tps_Mlg, Anticip := FALSE, Tps_Passe => #Tps_Passe) THEN
	                // Pour Affichage
	                #Melangeuse.Tps_Passe := TIME_TO_UDINT(#Tps_Passe)/1000;
	                #Melangeuse.Tps_Total := TIME_TO_UDINT(#Melangeuse.Pt_Zs._ParamMelang._Tps_Mlg)/1000;
	                RETURN;
	            END_IF;
	            #Ret_Maj_Niv := "Maj_Niv"(Num_Zone := #Melangeuse.CommunDos.Module_Commun._NumZone,
	                                      Num_Tache := #Melangeuse.CommunDos.Module_Commun._Tache,
	                                      Niveau := "NIV_MLG_PRESQ_FINI");
	            IF NOT "Tempo"(Tps_Picke := #Melangeuse.CommunDos.Module_Commun._TpsPicke, Duree := #Melangeuse.Pt_Zs._ParamMelang._Tps_Mlg_Ap_Inj, Anticip := FALSE, Tps_Passe => #Tps_Passe) THEN
	                RETURN;
	            END_IF;
	            #Melangeuse.CommunDos.Module_Commun._Etp := #Etp_Att_Aval_Pret;
	        END_REGION Cyclique Melange
	    #Etp_Att_Aval_Pret:
	        REGION Preliminaire Att Aval Pret
	            IF #Melangeuse.CommunDos.Module_Commun._Etp <> #Melangeuse.CommunDos.Module_Commun._EtpMemo THEN
	                #Melangeuse.CommunDos.Module_Commun._DescEtp := 'ATT AVAL PRET';
	                #Melangeuse.CommunDos.Module_Commun._EtpMemo := #Melangeuse.CommunDos.Module_Commun._Etp;
	            END_IF;
	        END_REGION Preliminaire Att Aval Pret
	        REGION Cyclique Att Aval Pret
	            IF NOT "FC_Test_Niv_AmontAval"(Num_Zone := #Melangeuse.CommunDos.Module_Commun._NumZone, Lst_Taches := #Melangeuse.Lst_Taches_Att_Res_Sous_Mlg) THEN
	                RETURN;
	            END_IF;
	            #Melangeuse.CommunDos.Module_Commun._Etp := #Etp_Deb_Vid;
	        END_REGION Cyclique Att Aval Pret
	    #Etp_Deb_Vid:
	        REGION Preliminaire Deb Vid
	            IF #Melangeuse.CommunDos.Module_Commun._Etp <> #Melangeuse.CommunDos.Module_Commun._EtpMemo THEN
	                #Melangeuse.CommunDos.Module_Commun._DescEtp := 'DEBUT VID.';
	                #Ret_Maj_Niv := "Maj_Niv"(Num_Zone := #Melangeuse.CommunDos.Module_Commun._NumZone,
	                                          Num_Tache := #Melangeuse.CommunDos.Module_Commun._Tache,
	                                          Niveau := "NIV_MLG_PRETE_VIDANGE");
	                #Melangeuse.CommunDos.Module_Commun._EtpMemo := #Melangeuse.CommunDos.Module_Commun._Etp;
	            END_IF;
	        END_REGION Preliminaire Deb Vid
	        REGION Cyclique Deb Vid
	            IF "Valid_Index"(#Melangeuse.Mob_Trappe) THEN
	                "FC_Posit_Mob"(Index_Mob := #Melangeuse.Mob_Trappe,
	                               Position := #Pos_Ouverture);
	            END_IF;
	            IF "Valid_Index"(#Melangeuse.Mob_Trappe) THEN
	                IF NOT "Test_Etat_Mobile"("Index_Mobile":= #Melangeuse.Mob_Trappe, Etat := "Pret") THEN
	                    RETURN;
	                END_IF;
	            END_IF;
	            IF "Valid_Index"(#Melangeuse.Mob_Verrou) THEN
	                "FC_Posit_Mob"(Index_Mob := #Melangeuse.Mob_Verrou,
	                               Position := #Pos_Ouverture);
	            END_IF;
	            IF "Valid_Index"(#Melangeuse.Mob_Vib) THEN
	                #Ret_Dem_Mobile := "Dem_Mobile"(#Melangeuse.Mob_Vib);
	            END_IF;
	            #Ret_Maj_Niv := "Maj_Niv"(Num_Zone := #Melangeuse.CommunDos.Module_Commun._NumZone,
	                                      Num_Tache := #Melangeuse.CommunDos.Module_Commun._Tache,
	                                      Niveau := "NIV_MLG_OUVERTE");
	            #Melangeuse.CommunDos.Module_Commun._Etp := #Etp_Att_Prd;
	        END_REGION Cyclique Deb Vid
	    #Etp_Att_Prd:
	        REGION Preliminaire Att Prd
	            IF #Melangeuse.CommunDos.Module_Commun._Etp <> #Melangeuse.CommunDos.Module_Commun._EtpMemo THEN
	                #Melangeuse.CommunDos.Module_Commun._DescEtp := 'ATT PRD';
	                #Melangeuse.CommunDos.Module_Commun._EtpMemo := #Melangeuse.CommunDos.Module_Commun._Etp;
	            END_IF;
	        END_REGION Preliminaire Att Prd
	        REGION Cyclique Att Prd
	            IF NOT "FC_Test_Niv_AmontAval"(Num_Zone := #Melangeuse.CommunDos.Module_Commun._NumZone, Lst_Taches := #Melangeuse.Lst_Taches_Att_Prd) THEN
	                RETURN;
	            END_IF;
	            #Melangeuse.CommunDos.Module_Commun._Etp := #Etp_Tempo_Vid_Melang;
	        END_REGION Cyclique Att Prd
	    #Etp_Tempo_Vid_Melang:
	        REGION Preliminaire Tempo Vid Melang
	            IF #Melangeuse.CommunDos.Module_Commun._Etp <> #Melangeuse.CommunDos.Module_Commun._EtpMemo THEN
	                #Melangeuse.CommunDos.Module_Commun._DescEtp := 'VIDANGE';
	                #Ret_Maj_Niv := "Maj_Niv"(Num_Zone := #Melangeuse.CommunDos.Module_Commun._NumZone,
	                                          Num_Tache := #Melangeuse.CommunDos.Module_Commun._Tache,
	                                          Niveau := "NIV_MLG_DEBUT_TEMPO_VIDANGE");
	                #Ret_RD_SYS_T := RD_SYS_T(#Melangeuse.CommunDos.Module_Commun._TpsPicke);
	                #Melangeuse.CommunDos.Module_Commun._EtpMemo := #Melangeuse.CommunDos.Module_Commun._Etp;
	            END_IF;
	        END_REGION Preliminaire Tempo Vid Melang
	        REGION Cyclique Tempo Vid Melang            
	            IF NOT "Tempo"(Tps_Picke := #Melangeuse.CommunDos.Module_Commun._TpsPicke, Duree := #Melangeuse.Tps_Vid_Melang, Anticip := FALSE, Tps_Passe => #Tps_Passe) THEN
	                // Pour Affichage
	                #Melangeuse.Tps_Passe := TIME_TO_UDINT(#Tps_Passe)/1000;
	                #Melangeuse.Tps_Total := TIME_TO_UDINT(#Melangeuse.Tps_Vid_Melang)/1000;
	                RETURN;
	            END_IF;
	            #Melangeuse.CommunDos.Module_Commun._Etp := #Etp_Att_Fin_Prd;
	        END_REGION Cyclique Tempo Vid Melang 
	   #Etp_Att_Fin_Prd:
	        REGION Preliminaire Att Fin Prd
	            IF #Melangeuse.CommunDos.Module_Commun._Etp <> #Melangeuse.CommunDos.Module_Commun._EtpMemo THEN
	                #Melangeuse.CommunDos.Module_Commun._DescEtp := 'ATT FIN PRD';
	                #Melangeuse.CommunDos.Module_Commun._EtpMemo := #Melangeuse.CommunDos.Module_Commun._Etp;
	            END_IF;
	        END_REGION Preliminaire Att Fin Prd
	        REGION Cyclique Att Fin Prd
	            IF NOT "FC_Test_Niv_AmontAval"(Num_Zone := #Melangeuse.CommunDos.Module_Commun._NumZone, Lst_Taches := #Melangeuse.Lst_Taches_Att_Fin_Prd) THEN
	                RETURN;
	            END_IF;
	            #Melangeuse.CommunDos.Module_Commun._Etp := #Etp_Ferm_Melang;
	        END_REGION Cyclique Att Fin Prd
	    #Etp_Ferm_Melang:
	        REGION Preliminaire Ferm Melang
	            IF #Melangeuse.CommunDos.Module_Commun._Etp <> #Melangeuse.CommunDos.Module_Commun._EtpMemo THEN
	                #Melangeuse.CommunDos.Module_Commun._DescEtp := 'FERM. MELANG.';
	                #Melangeuse.CommunDos.Module_Commun._EtpMemo := #Melangeuse.CommunDos.Module_Commun._Etp;
	            END_IF;
	        END_REGION Preliminaire Ferm Melang
	        REGION Cyclique Ferm Melang
	            IF "Valid_Index"(#Melangeuse.Mob_Trappe) THEN
	                "FC_Posit_Mob"(Index_Mob := #Melangeuse.Mob_Trappe,
	                               Position := #Pos_Fermeture);
	            END_IF;
	            IF "Valid_Index"(#Melangeuse.Mob_Trappe) THEN
	                IF NOT "Test_Etat_Mobile"("Index_Mobile" := #Melangeuse.Mob_Trappe, Etat := "Pret") THEN
	                    RETURN;
	                END_IF;
	            END_IF;
	            IF "Valid_Index"(#Melangeuse.Mob_Verrou) THEN
	                "FC_Posit_Mob"(Index_Mob := #Melangeuse.Mob_Verrou,
	                               Position := #Pos_Fermeture);
	            END_IF;
	            IF "Valid_Index"(#Melangeuse.Mob_Vib) THEN
	                #Ret_Arr_Imm_Mobile := "Arr_Imm_Mobile"(#Melangeuse.Mob_Vib);
	            END_IF;
	            IF "Valid_Index"(#Melangeuse.Mob_Melang) THEN
	                #Ret_Vid_Mobile := "Vid_Mobile"(#Melangeuse.Mob_Melang);
	            END_IF;
	            #Melangeuse.CommunDos.Module_Commun._Etp := #Etp_Fin_Lot;
	        END_REGION Cyclique Ferm Melang
	    #Etp_Fin_Lot:
	        REGION Preliminaire Fin Lot
	            IF #Melangeuse.CommunDos.Module_Commun._Etp <> #Melangeuse.CommunDos.Module_Commun._EtpMemo THEN
	                #Melangeuse.CommunDos.Module_Commun._DescEtp := 'FIN LOT';
	                #Melangeuse.CommunDos.Module_Commun._EtpMemo := #Melangeuse.CommunDos.Module_Commun._Etp;
	            END_IF;
	        END_REGION Preliminaire Fin Lot
	        REGION Cyclique Fin Lot
	            #Ret_Maj_Niv := "Maj_Niv"(Num_Zone := #Melangeuse.CommunDos.Module_Commun._NumZone,
	                                      Num_Tache := #Melangeuse.CommunDos.Module_Commun._Tache,
	                                      Niveau := "NIV_MLG_FIN_LOT");
	            #Melangeuse.CommunDos.Module_Commun._Etp := #Etp_Repos;
	        END_REGION Cyclique Fin Lot
	    #Etp_Att_Acq:
	        REGION Preliminaire Att Acq
	            IF #Melangeuse.CommunDos.Module_Commun._Etp <> #Melangeuse.CommunDos.Module_Commun._EtpMemo THEN
	                
	                #Melangeuse.CommunDos.Module_Commun._EtpMemo := #Melangeuse.CommunDos.Module_Commun._Etp;
	            END_IF;
	    END_REGION Preliminaire Att Acq 
	    REGION Cyclique Att Acq
	        #Melangeuse.Dial_Def.Reponse_Def := "FC_Recup_Rep_Def"(Index_Def := #Melangeuse.Dial_Def.Index_Defaut, Reponse_Ecrit => #Melangeuse.Dial_Def.Text_Reponse);
	        IF #Melangeuse.Dial_Def.Reponse_Def <> '' THEN
	            "FC_Raz_Def"(#Melangeuse.Dial_Def.Index_Defaut);
	            #Melangeuse.CommunDos.Module_Commun._Etp := #Melangeuse.CommunDos.Module_Commun._EtpPrec;
	            RETURN;
	        END_IF;
	    END_REGION Cyclique Att Acq
	END_CASE;
END_FUNCTION

