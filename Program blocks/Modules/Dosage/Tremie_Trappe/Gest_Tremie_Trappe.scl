FUNCTION "Gest_Tremie_Trappe" : Void
{ S7_Optimized_Access := 'TRUE' }
VERSION : 0.1
   VAR_INPUT 
      Fg_Base : Bool;
      RAZ_Pt_Zs : "S_Suivi_Dosage";
   END_VAR

   VAR_IN_OUT 
      Tremie_Trappe : "S_Tremie_Trappe";
   END_VAR

   VAR_TEMP 
      Ret_Test_Lot_Att_Tache : SInt;
      Ret_Maj_Niv : SInt;
      Ret_Dem_Mobile : SInt;
      Ret_Test_Etat_Mobile : SInt;
      Ret_Fin_Pos_Circuit : SInt;
      Ret_RD_SYS_T : Int;
      Tps_Act : Date_And_Time;
      Etat_Mobile : USInt;
      Ret_Vid_Mobile : SInt;
      Tps_Ecoule : Time;
      Ret_Arr_Imm_Mobile : SInt;
      Msg_Defaut : String;
   END_VAR

   VAR CONSTANT 
      Etp_Repos : SInt := 0;
      Etp_Att_Cond : SInt := 1;
      Etp_Att_Debut_Remp : SInt := 2;
      Etp_Att_Amont_Av_Test_Prd : SInt := 3;
      Etp_Att_Prd : SInt := 4;
      Etp_Fin_Amont : SInt := 5;
      Etp_Att_Aval_Pret : SInt := 6;
      Etp_Test_Trou : SInt := 7;
      Etp_Vidange : SInt := 8;
      Etp_Att_Fin_Amont_Ap_Vidange : SInt := 9;
      Etp_Att_Dem_Aval_Av_Test_Prd : SInt := 10;
      Etp_Tempo_Mini : SInt := 11;
      Etp_Att_Fin_Prd : SInt := 12;
      Etp_Att_Cond_Ferm_Trappe : SInt := 13;
      Etp_Fin_Vidange : SInt := 14;
      Etp_Fin_Lot : SInt := 15;
      Etp_Att_Acquittement : SInt := 16;
      POS_FERMETURE : SInt := 0;
      POS_OUVERTURE : SInt := 1;
   END_VAR


BEGIN
	//============= Affichage Etiquette =============//
	"FC_Affich_Etiquette"(Couleur := #Tremie_Trappe.Pt_Zs._SuiviCommun._Couleur,
	                      Cel_Dest := #Tremie_Trappe.Pt_Zs._Cel_Dest,
	                      CdMat := #Tremie_Trappe.Pt_Zs._SuiviCommun._CdMat,
	                      NoLot := #Tremie_Trappe.Pt_Zs._SuiviCommun._NoLot,
	                      QteLot := #Tremie_Trappe.Pt_Zs._SuiviCommun._QteLot,
	                      Desc_Etp := #Tremie_Trappe.DosCommun.Module_Commun._DescEtp,
	                      Etiquette := #Tremie_Trappe.DosCommun);
	//============= Affichage Etiquette =============//
	#Ret_RD_SYS_T := RD_SYS_T(#Tps_Act);
	CASE #Tremie_Trappe.DosCommun.Module_Commun._Etp OF
	    #Etp_Repos:
	        REGION Preliminaire Repos
	            IF #Tremie_Trappe.DosCommun.Module_Commun._Etp <> #Tremie_Trappe.DosCommun.Module_Commun._EtpMemo THEN
	                #Tremie_Trappe.DosCommun.Module_Commun._DescEtp := 'REPOS';
	                #Tremie_Trappe.Pt_Zs := #RAZ_Pt_Zs;
	                #Tremie_Trappe.DosCommun.Module_Commun._EtpPrec := #Tremie_Trappe.DosCommun.Module_Commun._Etp;
	                #Tremie_Trappe.DosCommun.Module_Commun._EtpMemo := #Tremie_Trappe.DosCommun.Module_Commun._Etp;
	            END_IF;
	        END_REGION Preliminaire Repos
	        REGION Cyclique Repos
	            #Ret_Test_Lot_Att_Tache := "Test_Lot_Att_Tache"(Num_Tache := #Tremie_Trappe.DosCommun.Module_Commun._Tache,
	                                                            Num_Zone => #Tremie_Trappe.DosCommun.Module_Commun._NumZone);
	            IF #Ret_Test_Lot_Att_Tache = 1 THEN
	                #Tremie_Trappe.Pt_Zs := "DB_Data_Dosage"."THIS"[#Tremie_Trappe.DosCommun.Module_Commun._NumZone];
	                #Ret_Maj_Niv := "Maj_Niv"(Num_Zone := #Tremie_Trappe.DosCommun.Module_Commun._NumZone,
	                                          Num_Tache := #Tremie_Trappe.DosCommun.Module_Commun._Tache,
	                                          Niveau := "NIV_TREMIE_TRAPPE_DEBUT_LOT");
	                #Tremie_Trappe.DosCommun.Module_Commun._Etp := #Etp_Att_Cond;
	                RETURN;
	            END_IF; 
	        END_REGION Cyclique Repos
	    #Etp_Att_Cond:
	        REGION Preliminaire Att Cond
	            IF #Tremie_Trappe.DosCommun.Module_Commun._Etp <> #Tremie_Trappe.DosCommun.Module_Commun._EtpMemo THEN
	                #Tremie_Trappe.DosCommun.Module_Commun._DescEtp := 'ATT COND';
	                #Tremie_Trappe.DosCommun.Module_Commun._EtpPrec := #Tremie_Trappe.DosCommun.Module_Commun._Etp;
	                #Tremie_Trappe.DosCommun.Module_Commun._EtpMemo := #Tremie_Trappe.DosCommun.Module_Commun._Etp;
	            END_IF;
	        END_REGION Preliminaire Att Cond
	        REGION Cyclique Att Cond
	            // Test Renseignement au moins d' une trappe sous tremie
	            IF NOT "Valid_Index"(#Tremie_Trappe.Mob_Trappe1) AND NOT "Valid_Index"(#Tremie_Trappe.Mob_Trappe2) THEN
	                RETURN;
	            END_IF;
	            // Test fermeture des trappes sous tremie
	            IF "Valid_Index"(#Tremie_Trappe.Mob_Trappe1) THEN
	                "FC_Posit_Mob"(Index_Mob := #Tremie_Trappe.Mob_Trappe1,
	                               Position := #POS_FERMETURE);
	            END_IF;
	            IF "Valid_Index"(#Tremie_Trappe.Mob_Trappe2) THEN
	                "FC_Posit_Mob"(Index_Mob := #Tremie_Trappe.Mob_Trappe2,
	                               Position := #POS_FERMETURE);
	            END_IF;
	            #Ret_Maj_Niv := "Maj_Niv"(Num_Zone:=#Tremie_Trappe.DosCommun.Module_Commun._NumZone,Num_Tache:=#Tremie_Trappe.DosCommun.Module_Commun._Tache,Niveau:="NIV_TREMIE_TRAPPE_AUT_REMP");
	            #Tremie_Trappe.DosCommun.Module_Commun._Etp := #Etp_Att_Debut_Remp;
	            RETURN;
	        END_REGION Cyclique Att Cond
	    #Etp_Att_Debut_Remp:
	        REGION Preliminaire Att Debut remp
	            IF #Tremie_Trappe.DosCommun.Module_Commun._Etp <> #Tremie_Trappe.DosCommun.Module_Commun._EtpMemo THEN
	                #Tremie_Trappe.DosCommun.Module_Commun._DescEtp := 'ATT REMP';
	                #Tremie_Trappe.DosCommun.Module_Commun._EtpPrec := #Tremie_Trappe.DosCommun.Module_Commun._Etp;
	                #Tremie_Trappe.DosCommun.Module_Commun._EtpMemo := #Tremie_Trappe.DosCommun.Module_Commun._Etp;
	            END_IF;
	        END_REGION Preliminaire Att Debut Remp
	        REGION Cyclique Att Debut Remp
	            // Attente Debut Remplissage Taches Amont
	            IF NOT "FC_Test_Niv_AmontAval"(Num_Zone:=#Tremie_Trappe.DosCommun.Module_Commun._NumZone, Lst_Taches:=#Tremie_Trappe.Lst_Att_Debut_Remp) THEN
	                RETURN;
	            END_IF;
	            // Test Renseignement Mobile Aspiration
	            IF "Valid_Index"(#Tremie_Trappe.Mob_Asp) THEN
	                #Ret_Dem_Mobile := "Dem_Mobile"(#Tremie_Trappe.Mob_Asp);
	            END_IF;
	            // Test Renseignement Mobile Decolmatage
	            IF "Valid_Index"(#Tremie_Trappe.Mob_Decol) THEN
	                #Ret_Dem_Mobile := "Dem_Mobile"(#Tremie_Trappe.Mob_Decol);
	            END_IF;
	            #Tremie_Trappe.DosCommun.Module_Commun._Etp := #Etp_Att_Prd;
	            RETURN;
	        END_REGION Cyclique Att Debut Remp
	    #Etp_Att_Amont_Av_Test_Prd:
	        REGION Preliminaire Att Amont Av Test Prd
	            IF #Tremie_Trappe.DosCommun.Module_Commun._Etp <> #Tremie_Trappe.DosCommun.Module_Commun._EtpMemo THEN
	                #Tremie_Trappe.DosCommun.Module_Commun._DescEtp := 'ATT AMONT';
	                #Tremie_Trappe.DosCommun.Module_Commun._EtpPrec := #Tremie_Trappe.DosCommun.Module_Commun._Etp;
	                #Tremie_Trappe.DosCommun.Module_Commun._EtpMemo := #Tremie_Trappe.DosCommun.Module_Commun._Etp;
	            END_IF;
	        END_REGION Preliminaire Att Amont Av Test Prd
	        REGION Cyclique Att Amont Av Test Prd
	            IF NOT "FC_Test_Niv_AmontAval"(Num_Zone := #Tremie_Trappe.DosCommun.Module_Commun._NumZone, Lst_Taches := #Tremie_Trappe.Lst_Att_Amont_Av_Test_Prd) THEN
	                RETURN;
	            END_IF;
	            #Tremie_Trappe.DosCommun.Module_Commun._Etp := #Etp_Att_Prd;
	        END_REGION Cyclique Att Amont Av Test Prd
	    #Etp_Att_Prd:
	        REGION Preliminaire Att Prd
	            IF #Tremie_Trappe.DosCommun.Module_Commun._Etp <> #Tremie_Trappe.DosCommun.Module_Commun._EtpMemo THEN
	                #Tremie_Trappe.DosCommun.Module_Commun._DescEtp := 'ATT PRD';
	                "Maj_Qte_Tache"(Num_Tache := #Tremie_Trappe.DosCommun.Module_Commun._Tache,
	                                Num_Zone := #Tremie_Trappe.DosCommun.Module_Commun._NumZone,
	                                Qte := "Calc_Qte_Tache"(Num_Zone := #Tremie_Trappe.DosCommun.Module_Commun._NumZone, Lst_Amont := #Tremie_Trappe.Lst_Taches_Amont_Qte));
	                #Tremie_Trappe.DosCommun.Module_Commun._EtpPrec := #Tremie_Trappe.DosCommun.Module_Commun._Etp;
	                //=== Traitement cas retour de defaut ===//
	                IF #Tremie_Trappe.Dial_Def.Reponse_Def <> '' THEN
	                    #Tremie_Trappe.DosCommun.Module_Commun._EtpMemo := #Tremie_Trappe.DosCommun.Module_Commun._Etp;
	                    CASE #Tremie_Trappe.Dial_Def.Num_Def_Etape OF
	                        1:
	                            GOTO Att_Prd_DEF1;
	                    END_CASE;
	                END_IF;
	                //=== Traitement cas retour de defaut ===//
	                #Ret_RD_SYS_T := RD_SYS_T(#Tremie_Trappe.DosCommun.Module_Commun._TpsPicke_Max);
	                #Tremie_Trappe.DosCommun.Module_Commun._EtpMemo := #Tremie_Trappe.DosCommun.Module_Commun._Etp;
	            END_IF;
	        END_REGION Preliminaire Att Prd
	        REGION Cyclique Att Prd 
	           // Tempo si non NB
	            IF "Tempo"(Duree := #Tremie_Trappe.Tps_MAX_Att_NB, Tps_Picke := #Tremie_Trappe.DosCommun.Module_Commun._TpsPicke_Max, Anticip := FALSE, Tps_Passe => #Tps_Ecoule) THEN
	                // DIALOG DEFAUT
	                #Tremie_Trappe.Dial_Def.Num_Def_Etape := 1;
	                #Msg_Defaut := #Msg_Defaut := CONCAT(IN1 := 'Defaut Recouvert Niveau Bas ', IN2 := #Tremie_Trappe.DosCommun.Module_Commun._NomTache, IN3 := ' !');
	                #Tremie_Trappe.Dial_Def.Index_Defaut := "Dialog_Defaut"(Fg_Base:=#Fg_Base, Msg_Defaut := #Msg_Defaut,
	                                                                       Type_Defaut := 0,
	                                                                       Automatisme := #Tremie_Trappe.Pt_Zs._SuiviCommun._CdProc,
	                                                                       Num_Lot := #Tremie_Trappe.Pt_Zs._SuiviCommun._NoLot,
	                                                                       Origine_Defaut := #Tremie_Trappe.DosCommun.Module_Commun._NomTache,
	                                                                       Libelle_Origine := #Tremie_Trappe.DosCommun.Module_Commun._NomTache,
	                                                                       Acq1 := 'Relance',
	                                                                       Acq2 := 'Abandon',
	                                                                       Acq3 := '',
	                                                                       Acq4 := '',
	                                                                       Acq5 := '',
	                                                                       Acq6 := '',
	                                                                       Acq7 := '',
	                                                                       Acq8 := '',
	                                                                       Acq_Choisi := -1,
	                                                                       Date_Arrivee := #Tps_Act);
	                #Tremie_Trappe.DosCommun.Module_Commun._Etp := #Etp_Att_Acquittement;
	                RETURN;
	            END_IF;
	        Att_Prd_DEF1:
	            //========================== Traitement des reponses defaut ==========================//
	            IF (#Tremie_Trappe.Dial_Def.Reponse_Def = 'Relance') THEN
	                // Reinitialisation de la tempo
	                #Ret_RD_SYS_T := RD_SYS_T(#Tremie_Trappe.DosCommun.Module_Commun._TpsPicke_Max);
	                // RAZ message defaut
	                #Tremie_Trappe.Dial_Def.Reponse_Def := '';
	                RETURN;
	            END_IF;
	            IF (#Tremie_Trappe.Dial_Def.Reponse_Def = 'Abandon') THEN
	                #Ret_Maj_Niv := "Maj_Niv"(Num_Zone := #Tremie_Trappe.DosCommun.Module_Commun._NumZone, Num_Tache := #Tremie_Trappe.DosCommun.Module_Commun._Tache, Niveau := "NIV_TREMIE_TRAPPE_NB");
	                // RAZ message defaut
	                #Tremie_Trappe.Dial_Def.Reponse_Def := '';
	                #Tremie_Trappe.DosCommun.Module_Commun._Etp := #Etp_Fin_Amont;
	                RETURN;
	            END_IF;
	            //========================== Traitement des reponses defaut ==========================//
	            // IF "Calc_Qte_Tache"(Num_Zone := #Tremie_Trappe.DosCommun.Module_Commun._NumZone, Lst_Amont := #Tremie_Trappe.Lst_Taches_Amont_Qte) = 0 THEN
	            //     #Tremie_Trappe.DosCommun.Module_Commun._Etp := #Etp_Fin_Lot;
	            //     RETURN;
	            // END_IF;
	            // Test Niveau Bas
	            IF NOT #Tremie_Trappe.NB AND (#Tremie_Trappe.Seuil_Poids_Test_NB < "Calc_Qte_Tache"(Num_Zone := #Tremie_Trappe.DosCommun.Module_Commun._NumZone, Lst_Amont := #Tremie_Trappe.Lst_Taches_Amont_Qte)) THEN
	                RETURN;
	            END_IF;
	            
	            #Tremie_Trappe.DosCommun.Module_Commun._Etp := #Etp_Fin_Amont;
	            RETURN;
	        END_REGION Cyclique Att Prd  
	    #Etp_Fin_Amont:
	        REGION Preliminaire Att Fin Amont
	            IF #Tremie_Trappe.DosCommun.Module_Commun._Etp <> #Tremie_Trappe.DosCommun.Module_Commun._EtpMemo THEN
	                #Tremie_Trappe.DosCommun.Module_Commun._DescEtp := 'FIN AMONT';
	                #Tremie_Trappe.DosCommun.Module_Commun._EtpPrec := #Tremie_Trappe.DosCommun.Module_Commun._Etp;
	                #Ret_Maj_Niv := "Maj_Niv"(Num_Zone := #Tremie_Trappe.DosCommun.Module_Commun._NumZone, Num_Tache := #Tremie_Trappe.DosCommun.Module_Commun._Tache, Niveau := "NIV_TREMIE_TRAPPE_PRES_PRD");
	                #Tremie_Trappe.DosCommun.Module_Commun._EtpMemo := #Tremie_Trappe.DosCommun.Module_Commun._Etp;
	            END_IF;
	        END_REGION  Preliminaire Att Fin Amont
	        REGION Cyclique Att Fin Amont
	            IF NOT "FC_Test_Niv_AmontAval"(Num_Zone := #Tremie_Trappe.DosCommun.Module_Commun._NumZone, Lst_Taches := #Tremie_Trappe.Lst_Att_Fin_Amont) THEN
	                RETURN;
	            END_IF;
	            IF "Valid_Index"(#Tremie_Trappe.Mob_Asp) THEN
	                #Ret_Vid_Mobile := "Vid_Mobile"(#Tremie_Trappe.Mob_Asp);
	            END_IF;
	            IF "Valid_Index"(#Tremie_Trappe.Mob_Decol) THEN
	                #Ret_Vid_Mobile := "Vid_Mobile"(#Tremie_Trappe.Mob_Decol);
	            END_IF;
	            #Tremie_Trappe.DosCommun.Module_Commun._Etp := #Etp_Att_Aval_Pret;
	            RETURN;
	        END_REGION Cyclique Att Fin Amont
	    #Etp_Att_Aval_Pret:
	        REGION Preliminaire Att Aval Pret
	            IF #Tremie_Trappe.DosCommun.Module_Commun._Etp <> #Tremie_Trappe.DosCommun.Module_Commun._EtpMemo THEN
	                #Tremie_Trappe.DosCommun.Module_Commun._DescEtp := 'ATT AVAL PRET';
	                #Tremie_Trappe.DosCommun.Module_Commun._EtpPrec := #Tremie_Trappe.DosCommun.Module_Commun._Etp;
	                #Tremie_Trappe.DosCommun.Module_Commun._EtpMemo := #Tremie_Trappe.DosCommun.Module_Commun._Etp;
	            END_IF;
	        END_REGION Preliminaire Att Aval Pret
	        REGION Cyclique Att Aval Pret
	            //Calcul debit massique  transporteur
	
	            IF NOT "FC_Test_Niv_AmontAval"(Num_Zone := #Tremie_Trappe.DosCommun.Module_Commun._NumZone, Lst_Taches := #Tremie_Trappe.Lst_Att_Aval_Pret) THEN
	                RETURN;
	            END_IF;
	           #Tremie_Trappe.DosCommun.Module_Commun._Etp := #Etp_Test_Trou;
	           RETURN;
	       END_REGION  Cyclique Att Aval Pret
	    #Etp_Test_Trou:
	        REGION Preliminaire Test Trou
	            IF #Tremie_Trappe.DosCommun.Module_Commun._Etp <> #Tremie_Trappe.DosCommun.Module_Commun._EtpMemo THEN
	                #Tremie_Trappe.DosCommun.Module_Commun._DescEtp := 'TEST TROU';
	                #Tremie_Trappe.DosCommun.Module_Commun._EtpPrec := #Tremie_Trappe.DosCommun.Module_Commun._Etp;
	                #Ret_Maj_Niv := "Maj_Niv"(Num_Zone := #Tremie_Trappe.DosCommun.Module_Commun._NumZone, Num_Tache := #Tremie_Trappe.DosCommun.Module_Commun._Tache, Niveau := "NIV_TREMIE_TRAPPE_ATT_FIN_TROU");
	                #Tremie_Trappe.DosCommun.Module_Commun._EtpMemo := #Tremie_Trappe.DosCommun.Module_Commun._Etp;
	            END_IF;
	        END_REGION Preliminaire Test Trou
	        REGION Cyclique Test Trou
	            IF NOT  "FC_Test_Niv_AmontAval"(Num_Zone := #Tremie_Trappe.DosCommun.Module_Commun._NumZone,Lst_Taches:=#Tremie_Trappe.Lst_Att_Fin_Trou) THEN
	                RETURN;
	            END_IF;
	            #Tremie_Trappe.DosCommun.Module_Commun._Etp := #Etp_Vidange;
	            RETURN;
	        END_REGION Cyclique Test Trou
	    #Etp_Vidange:
	        REGION Preliminaire Vidange
	            IF #Tremie_Trappe.DosCommun.Module_Commun._Etp <> #Tremie_Trappe.DosCommun.Module_Commun._EtpMemo THEN
	                #Tremie_Trappe.DosCommun.Module_Commun._DescEtp := 'VIDANGE';
	                #Tremie_Trappe.DosCommun.Module_Commun._EtpPrec := #Tremie_Trappe.DosCommun.Module_Commun._Etp;
	                #Tremie_Trappe.DosCommun.Module_Commun._EtpMemo := #Tremie_Trappe.DosCommun.Module_Commun._Etp;
	                "TremieTrappe_FC_Prelim_Vidange"(#Tremie_Trappe);
	               
	            END_IF;
	        END_REGION Preliminaire Vidange
	        REGION Cyclique Vidange
	            // Test Renseignement au moins d' une trappe sous tremie
	            IF NOT "Valid_Index"(#Tremie_Trappe.Mob_Trappe1) AND NOT "Valid_Index"(#Tremie_Trappe.Mob_Trappe2) THEN
	                RETURN;
	            END_IF;
	            IF "Valid_Index"(#Tremie_Trappe.Mob_Trappe1) THEN
	                "FC_Posit_Mob"(Index_Mob := #Tremie_Trappe.Mob_Trappe1,
	                               Position := #POS_OUVERTURE);
	            END_IF;
	            IF "Valid_Index"(#Tremie_Trappe.Mob_Trappe2) THEN
	                "FC_Posit_Mob"(Index_Mob := #Tremie_Trappe.Mob_Trappe2,
	                               Position := #POS_OUVERTURE);
	            END_IF;
	            #Ret_Maj_Niv := "Maj_Niv"(Num_Zone := #Tremie_Trappe.DosCommun.Module_Commun._NumZone, Num_Tache := #Tremie_Trappe.DosCommun.Module_Commun._Tache, Niveau := "NIV_TREMIE_TRAPPE_DEM_EXTRACT");
	            #Tremie_Trappe.DosCommun.Module_Commun._Etp := #Etp_Att_Fin_Amont_Ap_Vidange;
	            RETURN;
	        END_REGION Cyclique Vidange
	    #Etp_Att_Fin_Amont_Ap_Vidange:
	        REGION Preliminaire Att Fin Amont Ap Vidange
	            IF #Tremie_Trappe.DosCommun.Module_Commun._Etp <> #Tremie_Trappe.DosCommun.Module_Commun._EtpMemo THEN
	                #Tremie_Trappe.DosCommun.Module_Commun._DescEtp := 'ATT FIN AMONT';
	                #Tremie_Trappe.DosCommun.Module_Commun._EtpPrec := #Tremie_Trappe.DosCommun.Module_Commun._Etp;
	                #Tremie_Trappe.DosCommun.Module_Commun._EtpMemo := #Tremie_Trappe.DosCommun.Module_Commun._Etp;
	            END_IF;
	        END_REGION Preliminaire Att Fin Amont Ap Vidange
	        REGION Cyclique Att Fin Amont Ap Vidange
	            IF NOT "FC_Test_Niv_AmontAval"(Num_Zone := #Tremie_Trappe.DosCommun.Module_Commun._NumZone, Lst_Taches := #Tremie_Trappe.Lst_Att_Fin_Amont) THEN
	                RETURN;
	            END_IF;
	            #Tremie_Trappe.DosCommun.Module_Commun._Etp := #Etp_Att_Dem_Aval_Av_Test_Prd;
	            RETURN;
	        END_REGION Cyclique Att Fin Amont Ap Vidange
	    #Etp_Att_Dem_Aval_Av_Test_Prd:
	        REGION Preliminaire Att Dem Aval Av Test Prd
	            IF #Tremie_Trappe.DosCommun.Module_Commun._Etp <> #Tremie_Trappe.DosCommun.Module_Commun._EtpMemo THEN
	                #Tremie_Trappe.DosCommun.Module_Commun._DescEtp := 'ATT DEM AVAL';
	                #Tremie_Trappe.DosCommun.Module_Commun._EtpPrec := #Tremie_Trappe.DosCommun.Module_Commun._Etp;
	                #Tremie_Trappe.DosCommun.Module_Commun._EtpMemo := #Tremie_Trappe.DosCommun.Module_Commun._Etp;
	            END_IF;
	        END_REGION Preliminaire Att Dem Aval Av Test Prd
	        REGION Preliminaire Att Dem Aval Av Test Prd
	            IF NOT "FC_Test_Niv_AmontAval"(Num_Zone := #Tremie_Trappe.DosCommun.Module_Commun._NumZone, Lst_Taches := #Tremie_Trappe.Lst_Att_Dem_Aval_Av_Test_Fin_Prd) THEN
	                RETURN;
	            END_IF;
	            #Tremie_Trappe.DosCommun.Module_Commun._Etp := #Etp_Tempo_Mini;
	        END_REGION Preliminaire Att Dem Aval Av Test Prd
	    #Etp_Tempo_Mini:
	        REGION Preliminaire Att Tempo Mini
	            IF #Tremie_Trappe.DosCommun.Module_Commun._Etp <> #Tremie_Trappe.DosCommun.Module_Commun._EtpMemo THEN
	                #Tremie_Trappe.DosCommun.Module_Commun._EtpPrec := #Tremie_Trappe.DosCommun.Module_Commun._Etp;
	                #Ret_Maj_Niv := "Maj_Niv"(Num_Zone := #Tremie_Trappe.DosCommun.Module_Commun._NumZone, Num_Tache := #Tremie_Trappe.DosCommun.Module_Commun._Tache, Niveau := "NIV_TREMIE_TRAPPE_TEMPO_MINI");
	                #Ret_RD_SYS_T := RD_SYS_T(#Tremie_Trappe.DosCommun.Module_Commun._TpsPicke);
	                #Tremie_Trappe.DosCommun.Module_Commun._EtpMemo := #Tremie_Trappe.DosCommun.Module_Commun._Etp;
	            END_IF;
	        END_REGION Preliminaire Att Tempo Mini
	        REGION Cyclique Att Tempo Mini
	            // Attente de la tempo mini
	            IF NOT "Tempo"(Duree := #Tremie_Trappe.Tps_Mini, Tps_Picke := #Tremie_Trappe.DosCommun.Module_Commun._TpsPicke, Anticip := FALSE, Tps_Passe => #Tps_Ecoule) THEN
	                RETURN;
	            END_IF;
	            #Tremie_Trappe.DosCommun.Module_Commun._Etp := #Etp_Att_Fin_Prd;
	        END_REGION Cyclique Att Tempo Mini
	    #Etp_Att_Fin_Prd:
	        REGION Preliminaire Att Fin Prd
	            IF #Tremie_Trappe.DosCommun.Module_Commun._Etp <> #Tremie_Trappe.DosCommun.Module_Commun._EtpMemo THEN
	                #Tremie_Trappe.DosCommun.Module_Commun._DescEtp := 'ATT FIN PRD';
	                #Tremie_Trappe.DosCommun.Module_Commun._EtpPrec := #Tremie_Trappe.DosCommun.Module_Commun._Etp;
	                //=== Traitement cas retour de defaut ===//
	                IF #Tremie_Trappe.Dial_Def.Reponse_Def <> '' THEN
	                    #Tremie_Trappe.DosCommun.Module_Commun._EtpMemo := #Tremie_Trappe.DosCommun.Module_Commun._Etp;
	                    CASE #Tremie_Trappe.Dial_Def.Num_Def_Etape OF
	                        1:
	                            GOTO Att_Fin_Prd_DEF1;
	                    END_CASE;
	                END_IF;
	                //=== Traitement cas retour de defaut ===//
	                #Ret_RD_SYS_T := RD_SYS_T(#Tremie_Trappe.DosCommun.Module_Commun._TpsPicke);
	                #Ret_RD_SYS_T := RD_SYS_T(#Tremie_Trappe.DosCommun.Module_Commun._TpsPicke_Max);
	                #Tremie_Trappe.DosCommun.Module_Commun._EtpMemo := #Tremie_Trappe.DosCommun.Module_Commun._Etp;
	            END_IF;
	        END_REGION Preliminaire Att Fin Prd
	        REGION Cyclique Att Fin Prd
	            IF "Tempo"(Duree := #Tremie_Trappe.Tps_MAX_Att_NB, Tps_Picke := #Tremie_Trappe.DosCommun.Module_Commun._TpsPicke_Max, Anticip := FALSE, Tps_Passe => #Tps_Ecoule) THEN
	                // DIALOG DEFAUT
	                #Tremie_Trappe.Dial_Def.Num_Def_Etape := 1;
	                #Msg_Defaut := #Msg_Defaut := CONCAT(IN1 := 'Defaut: Decouvert Niveau Bas ', IN2 := #Tremie_Trappe.DosCommun.Module_Commun._NomTache, IN3 := ' !');
	                #Tremie_Trappe.Dial_Def.Index_Defaut := "Dialog_Defaut"(Fg_Base:=#Fg_Base, Msg_Defaut := #Msg_Defaut,
	                                                                       Type_Defaut := 0,
	                                                                       Automatisme := #Tremie_Trappe.Pt_Zs._SuiviCommun._CdProc,
	                                                                       Num_Lot := #Tremie_Trappe.Pt_Zs._SuiviCommun._NoLot,
	                                                                       Origine_Defaut := #Tremie_Trappe.DosCommun.Module_Commun._NomTache,
	                                                                       Libelle_Origine := #Tremie_Trappe.DosCommun.Module_Commun._NomTache,
	                                                                       Acq1 := 'Relance',
	                                                                       Acq2 := 'Abandon',
	                                                                       Acq3 := '',
	                                                                       Acq4 := '',
	                                                                       Acq5 := '',
	                                                                       Acq6 := '',
	                                                                       Acq7 := '',
	                                                                       Acq8 := '',
	                                                                       Acq_Choisi := -1,
	                                                                       Date_Arrivee := #Tps_Act);
	                #Tremie_Trappe.DosCommun.Module_Commun._Etp := #Etp_Att_Acquittement;
	                RETURN;
	            END_IF;
	        Att_Fin_Prd_DEF1:
	            //========================== Traitement des reponses defaut ==========================//
	            IF (#Tremie_Trappe.Dial_Def.Reponse_Def = 'Relance') THEN
	                // Reinitialisation de la tempo
	                #Ret_RD_SYS_T := RD_SYS_T(#Tremie_Trappe.DosCommun.Module_Commun._TpsPicke_Max);
	                // RAZ message defaut
	                #Tremie_Trappe.Dial_Def.Reponse_Def := '';
	                RETURN;
	            END_IF;
	            IF (#Tremie_Trappe.Dial_Def.Reponse_Def = 'Abandon') THEN
	                #Ret_Maj_Niv := "Maj_Niv"(Num_Zone := #Tremie_Trappe.DosCommun.Module_Commun._NumZone, Num_Tache := #Tremie_Trappe.DosCommun.Module_Commun._Tache, Niveau := "NIV_TREMIE_TRAPPE_NB");
	                // RAZ message defaut
	                #Tremie_Trappe.Dial_Def.Reponse_Def := '';
	                IF "Valid_Index"(#Tremie_Trappe.Mob_Vib) THEN
	                    #Ret_Dem_Mobile := "Dem_Mobile"(#Tremie_Trappe.Mob_Vib);
	                END_IF;
	                #Tremie_Trappe.DosCommun.Module_Commun._Etp := #Etp_Att_Cond_Ferm_Trappe;
	                RETURN;
	            END_IF;
	            //========================== Traitement des reponses defaut ==========================//
	            // Test Niveau Bas
	            IF #Tremie_Trappe.NB THEN
	                RETURN;
	            END_IF;
	            IF "Valid_Index"(#Tremie_Trappe.Mob_Vib) THEN
	                #Ret_Dem_Mobile := "Dem_Mobile"(#Tremie_Trappe.Mob_Vib);
	            END_IF;
	            #Tremie_Trappe.DosCommun.Module_Commun._Etp := #Etp_Att_Cond_Ferm_Trappe;
	        END_REGION Cyclique Att Fin Prd
	    #Etp_Att_Cond_Ferm_Trappe:
	        REGION Preliminaire Att Cond Ferm Trappe
	            IF #Tremie_Trappe.DosCommun.Module_Commun._Etp <> #Tremie_Trappe.DosCommun.Module_Commun._EtpMemo THEN
	                #Tremie_Trappe.DosCommun.Module_Commun._DescEtp := 'ATT COND FERM';
	                #Tremie_Trappe.DosCommun.Module_Commun._EtpPrec := #Tremie_Trappe.DosCommun.Module_Commun._Etp;
	                #Tremie_Trappe.DosCommun.Module_Commun._EtpMemo := #Tremie_Trappe.DosCommun.Module_Commun._Etp;
	            END_IF;
	        END_REGION Preliminaire Att Cond Ferm Trappe
	        REGION Cyclique Att Cond Ferm Trappe
	            IF NOT "FC_Cond_Fermeture_Trappe"() THEN
	                RETURN;
	            END_IF;
	            #Tremie_Trappe.DosCommun.Module_Commun._Etp := #Etp_Fin_Vidange;
	        END_REGION Cyclique Att Cond Ferm Trappe
	    #Etp_Fin_Vidange:
	        REGION Preliminaire Fin Vidange
	            IF #Tremie_Trappe.DosCommun.Module_Commun._Etp <> #Tremie_Trappe.DosCommun.Module_Commun._EtpMemo THEN
	                #Tremie_Trappe.DosCommun.Module_Commun._DescEtp := 'FIN VIDANGE';
	                #Tremie_Trappe.DosCommun.Module_Commun._EtpPrec := #Tremie_Trappe.DosCommun.Module_Commun._Etp;
	                #Ret_Maj_Niv := "Maj_Niv"(Num_Zone := #Tremie_Trappe.DosCommun.Module_Commun._NumZone,
	                                          Num_Tache := #Tremie_Trappe.DosCommun.Module_Commun._Tache,
	                                          Niveau := "NIV_TREMIE_TRAPPE_NIVEAU_BAS");
	                #Tremie_Trappe.DosCommun.Module_Commun._EtpMemo := #Tremie_Trappe.DosCommun.Module_Commun._Etp;
	            END_IF;
	        END_REGION Preliminaire Fin Vidange
	        REGION Cyclique Fin Vidange
	            IF "Valid_Index"(#Tremie_Trappe.Mob_Vib) THEN
	                #Ret_Arr_Imm_Mobile := "Arr_Imm_Mobile"(#Tremie_Trappe.Mob_Vib);
	            END_IF;
	            IF "Valid_Index"(#Tremie_Trappe.Mob_Trappe1) THEN
	                "FC_Posit_Mob"(Index_Mob := #Tremie_Trappe.Mob_Trappe1,
	                               Position := #POS_FERMETURE);
	            END_IF;
	            IF "Valid_Index"(#Tremie_Trappe.Mob_Trappe2) THEN
	                "FC_Posit_Mob"(Index_Mob := #Tremie_Trappe.Mob_Trappe2,
	                               Position := #POS_FERMETURE);
	            END_IF;
	            #Tremie_Trappe.DosCommun.Module_Commun._Etp := #Etp_Fin_Lot;
	        END_REGION Cyclique Fin Vidange
	    #Etp_Fin_Lot:
	        REGION Preliminaire Fin Lot
	            IF #Tremie_Trappe.DosCommun.Module_Commun._Etp <> #Tremie_Trappe.DosCommun.Module_Commun._EtpMemo THEN
	                #Tremie_Trappe.DosCommun.Module_Commun._DescEtp := 'FIN LOT';
	                #Tremie_Trappe.DosCommun.Module_Commun._EtpPrec := #Tremie_Trappe.DosCommun.Module_Commun._Etp;
	                #Tremie_Trappe.DosCommun.Module_Commun._EtpMemo := #Tremie_Trappe.DosCommun.Module_Commun._Etp;
	            END_IF;
	        END_REGION Preliminaire Fin Lot
	        REGION Cyclique Fin Lot
	            // Test Renseignement Mobile Aspiration
	            IF "Valid_Index"(#Tremie_Trappe.Mob_Asp) THEN
	                #Ret_Vid_Mobile := "Vid_Mobile"(#Tremie_Trappe.Mob_Asp);
	            END_IF;
	            // Test Renseignement Mobile Decolmatage
	            IF "Valid_Index"(#Tremie_Trappe.Mob_Decol) THEN
	                #Ret_Vid_Mobile := "Vid_Mobile"(#Tremie_Trappe.Mob_Decol);
	            END_IF;
	            #Ret_Maj_Niv := "Maj_Niv"(Num_Zone := #Tremie_Trappe.DosCommun.Module_Commun._NumZone, Num_Tache := #Tremie_Trappe.DosCommun.Module_Commun._Tache, Niveau := "NIV_TREMIE_TRAPPE_FIN_LOT");
	            #Tremie_Trappe.DosCommun.Module_Commun._Etp := #Etp_Repos;
	        END_REGION Cyclique Fin Lot
	    #Etp_Att_Acquittement:
	        REGION Preliminaire Att Acquittement
	            IF #Tremie_Trappe.DosCommun.Module_Commun._Etp <> #Tremie_Trappe.DosCommun.Module_Commun._EtpMemo THEN
	                #Tremie_Trappe.DosCommun.Module_Commun._DescEtp := 'ATT ACQ';
	                #Tremie_Trappe.DosCommun.Module_Commun._EtpMemo := #Tremie_Trappe.DosCommun.Module_Commun._Etp;
	            END_IF;
	        END_REGION Preliminaire Att Acquittement
	        REGION Cyclique Att Acquittement
	            #Tremie_Trappe.Dial_Def.Reponse_Def := "FC_Recup_Rep_Def"(Index_Def:=#Tremie_Trappe.Dial_Def.Index_Defaut, Reponse_Ecrit=> #Tremie_Trappe.Dial_Def.Text_Reponse);
	            IF #Tremie_Trappe.Dial_Def.Reponse_Def <> '' THEN
	                "FC_Raz_Def"(#Tremie_Trappe.Dial_Def.Index_Defaut);
	                #Tremie_Trappe.DosCommun.Module_Commun._Etp := #Tremie_Trappe.DosCommun.Module_Commun._EtpPrec;
	                RETURN;
	            END_IF;
	        END_REGION Cyclique Att Acquittement
	END_CASE;
END_FUNCTION

