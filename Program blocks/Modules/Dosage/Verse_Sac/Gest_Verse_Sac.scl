FUNCTION "Gest_Verse_Sac" : Void
{ S7_Optimized_Access := 'TRUE' }
VERSION : 0.1
   VAR_INPUT 
      Fg_Base : Bool;
      RAZ_Pt_Zs : "S_Suivi_Dosage";
   END_VAR

   VAR_IN_OUT 
      Verse_Sac : "S_Verse_Sac";
   END_VAR

   VAR_TEMP 
      Ret_Test_Lot_Att_Tache : SInt;
      Ret_Maj_Niv : SInt;
      i : USInt;
      k : SInt;
      Ret_Dem_Mobile : SInt;
      Ret_RD_SYS_T : Int;
      Msg_Defaut : String;
      Tps_Act : Date_And_Time;
      Tps_Passe : Time;
      Ret_Arr_Imm_Mobile : SInt;
      Ret_Vid_Mobile : SInt;
      Ret_Arret_Imm_Vidange : Bool;
   END_VAR

   VAR CONSTANT 
      Etp_Repos : SInt := 0;
      Etp_Att_Cond : SInt := 1;
      Etp_Dosage_Lance : SInt := 2;
      Etp_Vidange_Pesee : SInt := 3;
      Etp_Defaut_Vidange_Pesee : SInt := 4;
      Etp_Fin_Pesee : SInt := 5;
      Etp_Fin_Dos : SInt := 6;
      Etp_Att_Aut_Vidange : SInt := 7;
      Etp_Vidange : SInt := 8;
      Etp_Arret_Vidange : SInt := 9;
      Etp_Defaut_Vidange : SInt := 10;
      Etp_Controle_Poids : SInt := 11;
      Etp_Fin_Vidange : SInt := 12;
      Etp_Fin_Lot : SInt := 13;
      Etp_Att_Acq : SInt := 14;
   END_VAR


BEGIN
	#Ret_RD_SYS_T := RD_SYS_T(#Tps_Act);
	"FM_Bp_Tare"(CLK := #Verse_Sac.Bp_Tare OR #Verse_Sac.Affich_Vsac.Bp_Tare);
	"FM_Bp_Acq_Incor_Ben"(CLK := #Verse_Sac.Bp_Acq_Incor_Ben OR #Verse_Sac.Affich_Vsac.Bp_Acq_Incor_Ben);
	"FM_Bp_Acq_Part"(CLK := #Verse_Sac.Bp_Acq_Part OR #Verse_Sac.Affich_Vsac.Bp_Acq_Part);
	"FM_Bp_Acq_Total"(CLK := #Verse_Sac.Bp_Acq_Total OR #Verse_Sac.Affich_Vsac.Bp_Acq_Total);
	"FM_Bp_Acq_Vidange"(CLK := #Verse_Sac.Bp_Acq_Vidange OR #Verse_Sac.Affich_Vsac.Bp_Acq_Vidange);
	"FM_Bp_Sac_Entier"(CLK := #Verse_Sac.Bp_Sac_Entier OR #Verse_Sac.Affich_Vsac.Bp_Acq_Sac_Entier);
	//============= Affichage Etiquette =============//
	"FC_Affich_Etiquette"(Couleur := #Verse_Sac.Pt_Zs._SuiviCommun._Couleur,
	                      Cel_Dest := #Verse_Sac.Pt_Zs._Cel_Dest,
	                      CdMat := #Verse_Sac.Pt_Zs._SuiviCommun._CdMat,
	                      NoLot := #Verse_Sac.Pt_Zs._SuiviCommun._NoLot,
	                      QteLot := #Verse_Sac.Pt_Zs._SuiviCommun._QteLot,
	                      Desc_Etp := #Verse_Sac.DosCommun.Module_Commun._DescEtp,
	                      Etiquette := #Verse_Sac.DosCommun);
	//============= Affichage Etiquette =============//
	//============= Affichage Compo Supervision =============//
	"FC_Affich_Compo_Superv"(Code_MP := #Verse_Sac.Compo.Code_MP,
	                         Net := #Verse_Sac.Compo.Poids-#Verse_Sac.Compo.Ecart,
	                         Lst_Compos := #Verse_Sac.Lst_Compos,
	                         Lst_Compos_Affich := #Verse_Sac.Lst_Compo_Affich);
	//============= Affichage Compo Supervision =============//
	"FC_Affiche_VS"(#Verse_Sac);
	"Gest_Dosage_Lot"(Fg_Base:=#Fg_Base,
	                  Verse_Sac:=#Verse_Sac);
	CASE #Verse_Sac.DosCommun.Module_Commun._Etp OF
	    #Etp_Repos:
	        REGION Preliminaire Repos
	            IF #Verse_Sac.DosCommun.Module_Commun._Etp <>  #Verse_Sac.DosCommun.Module_Commun._EtpMemo THEN
	                #Verse_Sac.DosCommun.Module_Commun._DescEtp := 'REPOS';
	                #Verse_Sac.Pt_Zs := #RAZ_Pt_Zs;
	                #Verse_Sac.Compo := #RAZ_Pt_Zs._Lst_Compos[0];
	                #Verse_Sac.Msg_Oper := 'Attente Demande Dosage';
	                #Verse_Sac.DosCommun.Module_Commun._EtpMemo := #Verse_Sac.DosCommun.Module_Commun._Etp;
	            END_IF;
	        END_REGION Preliminaire Repos
	        REGION Cyclique Repos
	            // Attente lot a doser
	            #Ret_Test_Lot_Att_Tache := "Test_Lot_Att_Tache"(Num_Tache := #Verse_Sac.DosCommun.Module_Commun._Tache, Num_Zone => #Verse_Sac.DosCommun.Module_Commun._NumZone);
	            IF #Ret_Test_Lot_Att_Tache = 1 THEN
	                #Verse_Sac.Pt_Zs := "DB_Data_Dosage"."THIS"[#Verse_Sac.DosCommun.Module_Commun._NumZone];
	                // Recuperation des composants a doser dans ce lieux
	                FOR #i := 0 TO "Nbr_Max_Compo" DO
	                    IF #Verse_Sac.Pt_Zs._Lst_Compos[#i].Incorpo = #Verse_Sac.Lieu_Pes THEN
	                        #Verse_Sac.Lst_Compos[#k] := #Verse_Sac.Pt_Zs._Lst_Compos[#i];
	                        #k := #k + 1;
	                    END_IF;
	                END_FOR;
	                #Ret_Maj_Niv := "Maj_Niv"(Num_Zone := #Verse_Sac.DosCommun.Module_Commun._NumZone,
	                                          Num_Tache := #Verse_Sac.DosCommun.Module_Commun._Tache,
	                                          Niveau := "NIV_VS_DEBUT_LOT");
	                #Verse_Sac.DosCommun.Module_Commun._Etp := #Etp_Att_Cond;
	                RETURN;
	            END_IF;
	        END_REGION Cyclique Repos
	    #Etp_Att_Cond:
	        REGION Preliminaire Att Cond
	            IF #Verse_Sac.DosCommun.Module_Commun._Etp <> #Verse_Sac.DosCommun.Module_Commun._EtpMemo THEN
	                #Verse_Sac.DosCommun.Module_Commun._DescEtp := 'ATT COND';
	                #Verse_Sac.DosCommun.Module_Commun._EtpMemo := #Verse_Sac.DosCommun.Module_Commun._Etp;
	            END_IF;
	        END_REGION Preliminaire Att Cond
	        REGION Cyclique Att Cond
	            IF #Verse_Sac.Fg_Valid_Tol_Conf_Nut_Mat THEN
	                IF #Verse_Sac.Pt_Zs._Fg_Hop THEN 
	                    #Verse_Sac.DosCommun.Module_Commun._Etp:= #Etp_Fin_Lot;
	                END_IF;
	            END_IF;
	            IF "FC_Test_Niv_AmontAval"(Num_Zone := #Verse_Sac.DosCommun.Module_Commun._NumZone, Lst_Taches := #Verse_Sac.Lst_Taches_Att_Aff_Compo) THEN
	                #Verse_Sac.Msg_Oper := 'Attente Fin Dosage';
	            END_IF;
	            IF NOT "FC_Test_Niv_AmontAval"(Num_Zone := #Verse_Sac.DosCommun.Module_Commun._NumZone, Lst_Taches := #Verse_Sac.Lst_Taches_Att_Aut_Dem) THEN
	                RETURN;
	            END_IF;
	             #Verse_Sac.DosCommun.Module_Commun._Etp := #Etp_Dosage_Lance;
	            RETURN;
	        END_REGION Cyclique Att Cond   
	    #Etp_Dosage_Lance:
	        REGION Preliminaire Dosage Lance
	            IF #Verse_Sac.DosCommun.Module_Commun._Etp <> #Verse_Sac.DosCommun.Module_Commun._EtpMemo THEN
	                #Verse_Sac.DosCommun.Module_Commun._DescEtp := 'DOSAGE LANC.';
	                #Ret_Dem_Mobile := "Dem_Mobile"("Index_Mobile":= #Verse_Sac.Mob_Benne);
	                #Verse_Sac.Dem_Dos_Lot := 1;
	                #Verse_Sac.DosCommun.Module_Commun._EtpMemo := #Verse_Sac.DosCommun.Module_Commun._Etp;
	            END_IF;
	        END_REGION Preliminaire Dosage Lance
	        REGION Cyclique Dosage Lance
	            IF #Verse_Sac.Dem_Dos_Lot = 2 THEN
	                #Verse_Sac.DosCommun.Module_Commun._Etp := #Etp_Fin_Dos;
	                RETURN;
	            END_IF;
	            IF #Verse_Sac.Dem_Dos_Lot = 3 THEN
	                #Verse_Sac.Msg_Oper := 'Attente Autorisation Vidange';
	                IF "FC_Test_Niv_AmontAval"(Num_Zone := #Verse_Sac.DosCommun.Module_Commun._NumZone, Lst_Taches := #Verse_Sac.Lst_Taches_Att_Aval_Vid_Part) THEN
	                    #Verse_Sac.DosCommun.Module_Commun._Etp := #Etp_Vidange_Pesee;
	                    RETURN;
	                END_IF;
	            END_IF;
	        END_REGION Cyclique Dosage Lance
	    #Etp_Vidange_Pesee:
	        REGION Preliminaire Vidange Pesee
	            IF #Verse_Sac.DosCommun.Module_Commun._Etp <> #Verse_Sac.DosCommun.Module_Commun._EtpMemo THEN
	                #Verse_Sac.DosCommun.Module_Commun._DescEtp := 'VIDANGE PESEE';
	                #Verse_Sac.Msg_Oper := 'Attente Benne Vide';
	                #Verse_Sac.Memo_Poids_VS := #Verse_Sac.Poids_Benne;
	                #Ret_Maj_Niv := "Maj_Niv"(Num_Zone := #Verse_Sac.DosCommun.Module_Commun._NumZone,
	                                          Num_Tache := #Verse_Sac.DosCommun.Module_Commun._Tache,
	                                          Niveau := "NIV_VS_VIDANGE_PES_PARTIELLE");
	                IF NOT "Demarre_Vidange"(Verse_Sac := #Verse_Sac)THEN
	                    RETURN;
	                END_IF;
	                #Ret_Maj_Niv := "Maj_Niv"(Num_Zone := #Verse_Sac.DosCommun.Module_Commun._NumZone,
	                                          Num_Tache := #Verse_Sac.DosCommun.Module_Commun._Tache,
	                                          Niveau := "NIV_VS_VIDANGE_PES_PARTIELLE");
	                #Ret_RD_SYS_T := RD_SYS_T(#Verse_Sac.DosCommun.Module_Commun._TpsPicke);
	                #Ret_RD_SYS_T := RD_SYS_T(#Verse_Sac.DosCommun.Module_Commun._TpsPicke_Max);
	                #Verse_Sac.DosCommun.Module_Commun._EtpMemo := #Verse_Sac.DosCommun.Module_Commun._Etp;
	            END_IF;
	        END_REGION Preliminaire Vidange Pesee
	        REGION Cyclque Vidange Pesee
	            "Vidange_VS"(#Verse_Sac);
	            IF "Tempo"(Duree := #Verse_Sac.Tps_Vidange, Tps_Picke := #Verse_Sac.DosCommun.Module_Commun._TpsPicke, Anticip := FALSE, Tps_Passe => #Tps_Passe) THEN
	                #Verse_Sac.DosCommun.Module_Commun._Etp := #Etp_Fin_Vidange;
	            END_IF;
	            IF "Tempo"(Duree := #Verse_Sac.Tps_Max_Vidange, Tps_Picke := #Verse_Sac.DosCommun.Module_Commun._TpsPicke_Max, Anticip := FALSE, Tps_Passe => #Tps_Passe) THEN
	                #Verse_Sac.DosCommun.Module_Commun._Etp := #Etp_Defaut_Vidange;
	            END_IF;
	        END_REGION Cyclique Vidange Pesee
	    #Etp_Defaut_Vidange_Pesee:
	        REGION Preliminaire Defaut Vidange Pesee
	            IF #Verse_Sac.DosCommun.Module_Commun._Etp <> #Verse_Sac.DosCommun.Module_Commun._EtpMemo THEN
	                #Verse_Sac.DosCommun.Module_Commun._DescEtp := 'DEFAUT VIDANGE PESEE';
	                #Verse_Sac.DosCommun.Module_Commun._EtpPrec := #Verse_Sac.DosCommun.Module_Commun._Etp;
	                //=== Traitement cas retour de defaut ===//
	                IF #Verse_Sac.Dial_Def.Reponse_Def <> '' THEN
	                    #Verse_Sac.DosCommun.Module_Commun._EtpMemo := #Verse_Sac.DosCommun.Module_Commun._Etp;
	                    CASE #Verse_Sac.Dial_Def.Num_Def_Etape OF
	                        1:
	                            GOTO DEFAUT_VIDANGE_PESEE_DEF1;
	                    END_CASE;
	                END_IF;
	                //=== Traitement cas retour de defaut ===//
	                #Verse_Sac.Nbr_Extract := 0;
	                #Ret_RD_SYS_T := RD_SYS_T(#Verse_Sac.DosCommun.Module_Commun._TpsPicke);
	                #Verse_Sac.DosCommun.Module_Commun._EtpMemo := #Verse_Sac.DosCommun.Module_Commun._Etp;
	            END_IF;
	        END_REGION Preliminaire Defaut Vidange Pesee
	        REGION Cyclique Defaut Vidange Pesee
	            IF "Defaut_Vidange"(Etp_Vidange := #Etp_Vidange, Etp_Fin_Vidange := #Etp_Fin_Vidange, Verse_Sac := #Verse_Sac) THEN
	                #Verse_Sac.Dial_Def.Num_Def_Etape := 1;
	                #Msg_Defaut := CONCAT(IN1 := #Verse_Sac.Incor_Prep, IN2 := 'Impossible de vider correctement la benne');
	                #Verse_Sac.Dial_Def.Index_Defaut := "Dialog_Defaut"(Fg_Base := #Fg_Base, Msg_Defaut := #Msg_Defaut,
	                                                                    Type_Defaut := 0,
	                                                                    Automatisme := #Verse_Sac.DosCommun.Module_Commun._Autom,
	                                                                    Num_Lot := #Verse_Sac.DosCommun.Module_Commun._NumLot,
	                                                                    Origine_Defaut := #Verse_Sac.Incor_Prep,
	                                                                    Libelle_Origine := #Verse_Sac.DosCommun.Module_Commun._NomTache,
	                                                                    Acq1 := 'Relance',
	                                                                    Acq2 := 'Abandon',
	                                                                    Acq3 := '',
	                                                                    Acq4 := '',
	                                                                    Acq5 := '',
	                                                                    Acq6 := '',
	                                                                    Acq7 := '',
	                                                                    Acq8 := '',
	                                                                    Acq_Choisi := -1,
	                                                                    Date_Arrivee := #Tps_Act);
	                #Verse_Sac.DosCommun.Module_Commun._Etp := #Etp_Att_Acq;
	            END_IF;
	        DEFAUT_VIDANGE_PESEE_DEF1:
	            //========================== Traitement des reponses defaut ==========================//
	            IF (#Verse_Sac.Dial_Def.Reponse_Def = 'Relance') THEN
	                #Verse_Sac.DosCommun.Module_Commun._Etp := #Etp_Vidange;
	                #Verse_Sac.Dial_Def.Reponse_Def := '';
	                RETURN;
	            END_IF;
	            IF (#Verse_Sac.Dial_Def.Reponse_Def = 'Abandon') THEN
	                #Verse_Sac.DosCommun.Module_Commun._Etp := #Etp_Fin_Vidange;
	                #Verse_Sac.Dial_Def.Reponse_Def := '';
	                RETURN;
	            END_IF;
	            //========================== Traitement des reponses defaut ==========================//
	        END_REGION Cyclique Defaut Vidange Pesee
	    #Etp_Fin_Pesee:
	        REGION Preliminaire Fin Pesee
	            IF #Verse_Sac.DosCommun.Module_Commun._Etp <> #Verse_Sac.DosCommun.Module_Commun._EtpMemo THEN
	                #Verse_Sac.DosCommun.Module_Commun._DescEtp := 'FIN PESEE';
	                #Verse_Sac.DosCommun.Module_Commun._EtpMemo := #Verse_Sac.DosCommun.Module_Commun._Etp;
	            END_IF;
	        END_REGION Preliminaire Fin Pesee
	        REGION Cyclique Fin Pesee
	            IF "Arret_Vidange"(#Verse_Sac) THEN
	                #Verse_Sac.DosCommun.Module_Commun._Etp := #Etp_Dosage_Lance;
	            END_IF;
	        END_REGION Cyclique Fin Pesee
	    #Etp_Fin_Dos:
	        REGION Preliminaire Fin Dos
	            IF #Verse_Sac.DosCommun.Module_Commun._Etp <> #Verse_Sac.DosCommun.Module_Commun._EtpMemo THEN
	                #Verse_Sac.DosCommun.Module_Commun._DescEtp := 'FIN DOSAGE';
	                #Ret_Maj_Niv := "Maj_Niv"(Num_Zone := #Verse_Sac.DosCommun.Module_Commun._NumZone,
	                                          Num_Tache := #Verse_Sac.DosCommun.Module_Commun._Tache,
	                                          Niveau := "NIV_VS_FIN_INCOR");
	                #Verse_Sac.Msg_Oper := 'Attente Fin Dosage';
	                #Verse_Sac.DosCommun.Module_Commun._EtpMemo := #Verse_Sac.DosCommun.Module_Commun._Etp;
	            END_IF;
	        END_REGION Preliminaire Fin Dos
	        REGION Cyclique Fin Dos
	            IF NOT "FC_Test_Niv_AmontAval"(Num_Zone := #Verse_Sac.DosCommun.Module_Commun._NumZone, Lst_Taches := #Verse_Sac.Lst_Taches_Att_Aut_Vidange) THEN
	                RETURN;
	            END_IF;
	            IF #Verse_Sac.Fg_Sans_Vidange THEN
	                #Ret_Arr_Imm_Mobile := "Arr_Imm_Mobile"(#Verse_Sac.Mob_Aspiration);
	                #Ret_Vid_Mobile := "Vid_Mobile"(#Verse_Sac.Mob_Decolmatage);
	                #Verse_Sac.DosCommun.Module_Commun._Etp := #Etp_Controle_Poids;
	            ELSE
	                #Verse_Sac.DosCommun.Module_Commun._Etp := #Etp_Vidange;
	            END_IF;
	        END_REGION Cyclique Fin Dos
	    #Etp_Att_Aut_Vidange:
	        REGION Preliminaire Att Aut Vidange
	            IF #Verse_Sac.DosCommun.Module_Commun._Etp <> #Verse_Sac.DosCommun.Module_Commun._EtpMemo THEN
	                #Verse_Sac.DosCommun.Module_Commun._DescEtp := 'ATT AUT VID.';
	                #Verse_Sac.Msg_Oper := 'Attente Fin Vidange';
	                #Verse_Sac.DosCommun.Module_Commun._EtpMemo := #Verse_Sac.DosCommun.Module_Commun._Etp;
	            END_IF;
	        END_REGION Preliminaire Att Aut Vidange
	        REGION Cyclique Att Aut Vidange
	            IF NOT "FC_Test_Niv_AmontAval"(Num_Zone := #Verse_Sac.DosCommun.Module_Commun._NumZone, Lst_Taches := #Verse_Sac.Lst_Taches_Att_Aut_Vidange) THEN
	                RETURN;
	            END_IF;
	            #Verse_Sac.DosCommun.Module_Commun._Etp := #Etp_Vidange;
	            RETURN;
	        END_REGION Cyclique Att Aut Vidange
	    #Etp_Vidange:
	        REGION Preliminaire Vidange
	            IF #Verse_Sac.DosCommun.Module_Commun._Etp <> #Verse_Sac.DosCommun.Module_Commun._EtpMemo THEN
	                #Verse_Sac.DosCommun.Module_Commun._DescEtp := 'VIDANGE';
	                #Verse_Sac.Msg_Oper := 'Attente Benne Vide';
	                #Verse_Sac.Memo_Poids_VS := #Verse_Sac.Poids_Benne;
	                #Ret_Maj_Niv := "Maj_Niv"(Num_Zone := #Verse_Sac.DosCommun.Module_Commun._NumZone,
	                                          Num_Tache := #Verse_Sac.DosCommun.Module_Commun._Tache,
	                                          Niveau := "NIV_VS_DEBUT_VIDANGE");
	                IF NOT "Demarre_Vidange"(#Verse_Sac) THEN
	                    RETURN;
	                END_IF;
	                #Ret_RD_SYS_T := RD_SYS_T(#Verse_Sac.DosCommun.Module_Commun._TpsPicke);
	                #Ret_RD_SYS_T := RD_SYS_T(#Verse_Sac.DosCommun.Module_Commun._TpsPicke_Max);
	                #Verse_Sac.DosCommun.Module_Commun._EtpMemo := #Verse_Sac.DosCommun.Module_Commun._Etp;
	            END_IF;
	        END_REGION Preliminaire Vidange
	        REGION Cyclique Vidange
	            "Vidange_VS"(#Verse_Sac);
	            IF "Tempo"(Duree := #Verse_Sac.Tps_Vidange, Tps_Picke := #Verse_Sac.DosCommun.Module_Commun._TpsPicke, Anticip := FALSE, Tps_Passe => #Tps_Passe) THEN
	                #Verse_Sac.DosCommun.Module_Commun._Etp := #Etp_Fin_Vidange;
	            END_IF;
	            IF "Tempo"(Duree := #Verse_Sac.Tps_Max_Vidange, Tps_Picke := #Verse_Sac.DosCommun.Module_Commun._TpsPicke_Max, Anticip := FALSE, Tps_Passe => #Tps_Passe) THEN
	                #Verse_Sac.DosCommun.Module_Commun._Etp := #Etp_Defaut_Vidange;
	            END_IF;
	        END_REGION Cyclique Vidange
	    #Etp_Arret_Vidange:
	        REGION Preliminaire Arret Vidange
	            IF #Verse_Sac.DosCommun.Module_Commun._Etp <> #Verse_Sac.DosCommun.Module_Commun._EtpMemo THEN
	                #Verse_Sac.DosCommun.Module_Commun._DescEtp := 'ARRET VIDANGE';
	                IF #Verse_Sac.Type_Acq_Incor = 6 THEN
	                    #Verse_Sac.Msg_Oper := 'Attente Incorporation Benne';
	                END_IF;
	                #Verse_Sac.DosCommun.Module_Commun._EtpMemo := #Verse_Sac.DosCommun.Module_Commun._Etp;
	            END_IF;
	        END_REGION Preliminaire Arret Vidange
	        REGION Cyclique Arret Vidange
	            IF #Verse_Sac.Type_Acq_Incor <> 6 OR "FM_Bp_Acq_Incor_Ben".Q THEN
	                IF #Verse_Sac.Fg_Ctrl_Poids_Fin_Lot OR "Arret_Vidange"(#Verse_Sac) THEN
	                    #Verse_Sac.DosCommun.Module_Commun._Etp := #Etp_Controle_Poids;
	                END_IF;
	            END_IF;
	        END_REGION Cyclique Arret Vidange
	    #Etp_Defaut_Vidange:
	        REGION Preliminaire Defaut Vidange
	            IF #Verse_Sac.DosCommun.Module_Commun._Etp <> #Verse_Sac.DosCommun.Module_Commun._EtpMemo THEN
	                #Verse_Sac.DosCommun.Module_Commun._DescEtp := 'DEFAUT VIDANGE';
	                #Verse_Sac.DosCommun.Module_Commun._EtpPrec := #Verse_Sac.DosCommun.Module_Commun._Etp;
	                //=== Traitement cas retour de defaut ===//
	                IF #Verse_Sac.Dial_Def.Reponse_Def <> '' THEN
	                    #Verse_Sac.DosCommun.Module_Commun._EtpMemo := #Verse_Sac.DosCommun.Module_Commun._Etp;
	                    CASE #Verse_Sac.Dial_Def.Num_Def_Etape OF
	                        1:
	                            GOTO DEFAUT_VIDANGE_DEF1;
	                    END_CASE;
	                END_IF;
	                //=== Traitement cas retour de defaut ===//
	                #Verse_Sac.Nbr_Extract := 0;
	                #Verse_Sac.DosCommun.Module_Commun._EtpMemo := #Verse_Sac.DosCommun.Module_Commun._Etp;
	            END_IF;
	        END_REGION Preliminaire Defaut Vidange
	        REGION Cyclique Defaut Vidange
	            IF "Defaut_Vidange"(Etp_Vidange := #Etp_Vidange, Etp_Fin_Vidange := #Etp_Fin_Vidange, Verse_Sac := #Verse_Sac) THEN
	                #Verse_Sac.Dial_Def.Num_Def_Etape := 1;
	                #Msg_Defaut := CONCAT(IN1 := #Verse_Sac.Incor_Prep, IN2 := 'Impossible de vider correctement la benne');
	                #Verse_Sac.Dial_Def.Index_Defaut := "Dialog_Defaut"(Fg_Base := #Fg_Base, Msg_Defaut := #Msg_Defaut,
	                                                                    Type_Defaut := 0,
	                                                                    Automatisme := #Verse_Sac.Pt_Zs._SuiviCommun._CdProc,
	                                                                    Num_Lot := #Verse_Sac.Pt_Zs._SuiviCommun._NoLot,
	                                                                    Origine_Defaut := #Verse_Sac.Incor_Prep,
	                                                                    Libelle_Origine := #Verse_Sac.DosCommun.Module_Commun._NomTache,
	                                                                    Acq1 := 'Relance',
	                                                                    Acq2 := 'Abandon',
	                                                                    Acq3 := '',
	                                                                    Acq4 := '',
	                                                                    Acq5 := '',
	                                                                    Acq6 := '',
	                                                                    Acq7 := '',
	                                                                    Acq8 := '',
	                                                                    Acq_Choisi := -1,
	                                                                    Date_Arrivee := #Tps_Act);
	                #Verse_Sac.DosCommun.Module_Commun._Etp := #Etp_Att_Acq;
	            END_IF;
	        DEFAUT_VIDANGE_DEF1:
	            //========================== Traitement des reponses defaut ==========================//
	            IF (#Verse_Sac.Dial_Def.Reponse_Def = 'Relance') THEN
	                #Verse_Sac.DosCommun.Module_Commun._Etp := #Etp_Vidange;
	                #Verse_Sac.Dial_Def.Reponse_Def := '';
	                RETURN;
	            END_IF;
	            IF (#Verse_Sac.Dial_Def.Reponse_Def = 'Abandon') THEN
	                #Verse_Sac.DosCommun.Module_Commun._Etp := #Etp_Fin_Vidange;
	                #Verse_Sac.Dial_Def.Reponse_Def := '';
	                RETURN;
	            END_IF;
	            //========================== Traitement des reponses defaut ==========================//
	        END_REGION Cyclique Defaut Vidange
	    #Etp_Controle_Poids:
	        REGION Preliminaire Controle Poids
	            IF #Verse_Sac.DosCommun.Module_Commun._Etp <> #Verse_Sac.DosCommun.Module_Commun._EtpMemo THEN
	                #Verse_Sac.DosCommun.Module_Commun._DescEtp := 'CONTROLE POIDS';
	                #Verse_Sac.DosCommun.Module_Commun._EtpPrec := #Verse_Sac.DosCommun.Module_Commun._Etp;
	                //=== Traitement cas retour de defaut ===//
	                IF #Verse_Sac.Dial_Def.Reponse_Def <> '' THEN
	                    #Verse_Sac.DosCommun.Module_Commun._EtpMemo := #Verse_Sac.DosCommun.Module_Commun._Etp;
	                    CASE #Verse_Sac.Dial_Def.Num_Def_Etape OF
	                        1:
	                            GOTO CONTROLE_POIDS_DEF1;
	                    END_CASE;
	                END_IF;
	                //=== Traitement cas retour de defaut ===//
	                #Ret_Maj_Niv := "Maj_Niv"(Num_Zone := #Verse_Sac.DosCommun.Module_Commun._NumZone,
	                                          Num_Tache := #Verse_Sac.DosCommun.Module_Commun._Tache,
	                                          Niveau := "NIV_VS_FIN_SYNCHRO");
	                IF NOT #Verse_Sac.Fg_Ctrl_Poids_Fin_Lot THEN
	                    #Verse_Sac.DosCommun.Module_Commun._Etp := #Etp_Fin_Dos;
	                END_IF;
	                #Verse_Sac.Msg_Oper := 'Controle Poids Benne';
	                #Ret_RD_SYS_T := RD_SYS_T(#Verse_Sac.DosCommun.Module_Commun._TpsPicke);
	                #Ret_RD_SYS_T := RD_SYS_T(#Verse_Sac.DosCommun.Module_Commun._TpsPicke_Max);
	                #Verse_Sac.DosCommun.Module_Commun._EtpMemo := #Verse_Sac.DosCommun.Module_Commun._Etp;
	            END_IF;
	        END_REGION Preliminaire Controle Poids
	        REGION Cyclique Controle Poids
	            IF "Tempo"(Duree:=#Verse_Sac.Tps_Ctrl_Pds_Fin_Lot, Tps_Picke:=#Verse_Sac.DosCommun.Module_Commun._TpsPicke, Anticip:=FALSE, Tps_Passe=>#Tps_Passe) THEN
	                #Verse_Sac.DosCommun.Module_Commun._Etp := #Etp_Fin_Vidange;
	            END_IF;
	            IF "Tempo"(Duree:=#Verse_Sac.Tps_Max_Ctrl_Pds_Fin_Lot, Tps_Picke:=#Verse_Sac.DosCommun.Module_Commun._TpsPicke_Max, Anticip:=FALSE, Tps_Passe=>#Tps_Passe) THEN
	                #Ret_Arret_Imm_Vidange := "Arret_Imm_Vidange"(#Verse_Sac);
	                #Verse_Sac.Dial_Def.Num_Def_Etape := 1;
	                #Msg_Defaut := CONCAT(IN1 := 'Poids de la benne incorect, poids benne: ', IN2 :=REAL_TO_STRING(#Verse_Sac.Poids_Benne), IN3 :='Veuillez verifier que tous les composants sont dans la benne' );
	                #Verse_Sac.Dial_Def.Index_Defaut := "Dialog_Defaut"(Fg_Base := #Fg_Base, Msg_Defaut := #Msg_Defaut,
	                                                                    Type_Defaut := 0,
	                                                                    Automatisme := #Verse_Sac.Pt_Zs._SuiviCommun._CdProc,
	                                                                    Num_Lot := #Verse_Sac.Pt_Zs._SuiviCommun._NoLot,
	                                                                    Origine_Defaut := #Verse_Sac.Incor_Prep,
	                                                                    Libelle_Origine := #Verse_Sac.DosCommun.Module_Commun._NomTache,
	                                                                    Acq1 := 'Relance',
	                                                                    Acq2 := 'Abandon',
	                                                                    Acq3 := '',
	                                                                    Acq4 := '',
	                                                                    Acq5 := '',
	                                                                    Acq6 := '',
	                                                                    Acq7 := '',
	                                                                    Acq8 := '',
	                                                                    Acq_Choisi := -1,
	                                                                    Date_Arrivee := #Tps_Act);
	                #Verse_Sac.DosCommun.Module_Commun._Etp := #Etp_Att_Acq;
	            END_IF;
	        CONTROLE_POIDS_DEF1:
	            //========================== Traitement des reponses defaut ==========================//
	            IF (#Verse_Sac.Dial_Def.Reponse_Def = 'Relance') THEN
	                #Ret_RD_SYS_T := RD_SYS_T(#Verse_Sac.DosCommun.Module_Commun._TpsPicke);
	                #Ret_RD_SYS_T := RD_SYS_T(#Verse_Sac.DosCommun.Module_Commun._TpsPicke_Max);
	                #Verse_Sac.Dial_Def.Reponse_Def := '';
	                RETURN;
	            END_IF;
	            IF (#Verse_Sac.Dial_Def.Reponse_Def = 'Abandon') THEN
	                #Verse_Sac.DosCommun.Module_Commun._Etp := #Etp_Fin_Vidange;
	                #Verse_Sac.Dial_Def.Reponse_Def := '';
	                RETURN;
	            END_IF;
	            //========================== Traitement des reponses defaut ==========================//
	        END_REGION Cyclique Controle Poids
	    #Etp_Fin_Vidange:
	        REGION Preliminaire Fin Vidange
	            IF #Verse_Sac.DosCommun.Module_Commun._Etp <> #Verse_Sac.DosCommun.Module_Commun._EtpMemo THEN
	                #Verse_Sac.DosCommun.Module_Commun._DescEtp := 'FIN VIDANGE';
	                IF #Verse_Sac.Fg_Ctrl_Poids_Fin_Lot AND NOT "Arret_Vidange"(#Verse_Sac) THEN
	                    RETURN;
	                END_IF;
	                
	                #Ret_Maj_Niv := "Maj_Niv"(Num_Zone := #Verse_Sac.DosCommun.Module_Commun._NumZone,
	                                          Num_Tache := #Verse_Sac.DosCommun.Module_Commun._Tache,
	                                          Niveau := "NIV_VS_FIN_VIDANGE");
	                #Verse_Sac.DosCommun.Module_Commun._EtpMemo := #Verse_Sac.DosCommun.Module_Commun._Etp;
	            END_IF;
	        END_REGION Preliminaire Fin Vidange
	        REGION Cyclique Fin Vidange
	            IF NOT "FC_Test_Niv_AmontAval"(Num_Zone := #Verse_Sac.DosCommun.Module_Commun._NumZone, Lst_Taches := #Verse_Sac.Lst_Taches_Att_Fin_Dosage) THEN
	                RETURN;
	            END_IF;
	            #Verse_Sac.DosCommun.Module_Commun._Etp := #Etp_Fin_Lot;
	            RETURN;
	        END_REGION Cyclique Fin Vidange
	    #Etp_Fin_Lot:
	        REGION Preliminaire Fin Lot
	            IF #Verse_Sac.DosCommun.Module_Commun._Etp <> #Verse_Sac.DosCommun.Module_Commun._EtpMemo THEN
	                #Verse_Sac.DosCommun.Module_Commun._DescEtp := 'FIN LOT';
	                #Verse_Sac.DosCommun.Module_Commun._EtpMemo := #Verse_Sac.DosCommun.Module_Commun._Etp;
	            END_IF;
	        END_REGION Preliminaire Fin Lot
	        REGION Cyclique Fin Lot
	            #Ret_Arr_Imm_Mobile := "Arr_Imm_Mobile"(#Verse_Sac.Mob_Benne);
	            #Ret_Maj_Niv := "Maj_Niv"(Num_Zone := #Verse_Sac.DosCommun.Module_Commun._NumZone,
	                                      Num_Tache := #Verse_Sac.DosCommun.Module_Commun._Tache,
	                                      Niveau := "NIV_VS_FIN_LOT");
	            #Verse_Sac.DosCommun.Module_Commun._Etp := #Etp_Repos;
	            RETURN;
	        END_REGION Cyclique Fin Lot
	    #Etp_Att_Acq:
	        REGION Preliminaire Att Acq
	            IF #Verse_Sac.DosCommun.Module_Commun._Etp <> #Verse_Sac.DosCommun.Module_Commun._EtpMemo THEN
	                #Verse_Sac.DosCommun.Module_Commun._DescEtp := 'ATT ACQ';
	                #Verse_Sac.DosCommun.Module_Commun._EtpMemo := #Verse_Sac.DosCommun.Module_Commun._Etp;
	            END_IF;
	        END_REGION Preliminaire Att Acq
	        REGION Cyclique Att Acq
	            #Verse_Sac.Dial_Def.Reponse_Def := "FC_Recup_Rep_Def"(Index_Def := #Verse_Sac.Dial_Def.Index_Defaut, Reponse_Ecrit => #Verse_Sac.Dial_Def.Text_Reponse);
	            IF #Verse_Sac.Dial_Def.Reponse_Def <> '' THEN
	                "FC_Raz_Def"(#Verse_Sac.Dial_Def.Index_Defaut);
	                #Verse_Sac.DosCommun.Module_Commun._Etp := #Verse_Sac.DosCommun.Module_Commun._EtpPrec;
	                RETURN;
	            END_IF;
	        END_REGION Cyclique Att Acq 
	END_CASE;
END_FUNCTION

