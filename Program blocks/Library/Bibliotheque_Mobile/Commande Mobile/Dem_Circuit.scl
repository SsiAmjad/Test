FUNCTION "Dem_Circuit" : SInt
TITLE = Demarrage circuit
{ S7_Optimized_Access := 'TRUE' }
AUTHOR : 'I.Daoudi'
FAMILY : Mobile
VERSION : 1.0
//Demarrage circuit
   VAR_INPUT 
      Circuit : "S_Circuit";
   END_VAR

   VAR_TEMP 
      i : USInt;
      Ret_Test_Valid_Index : SInt;
      Ret_Dem_Mobile : SInt;
      Ret_Reserv_Mobile : SInt;
   END_VAR


BEGIN
	// +---------+------------+-------------+------------------------------------------------------------------------+
	// | Version | Date       | Autheur     | Ajouts/Modifications                                                   |
	// +---------+------------+-------------+------------------------------------------------------------------------+
	// | V_1     | 15.08.2023 | I.DAOUDI    | Creation du block                                                      |
	// +---------+------------+-------------+------------------------------------------------------------------------+
	// Test du Valid_Index
	IF NOT "Valid_Index"(#Circuit.Mob_Circuit[0].Mobile) THEN
	    #Dem_Circuit := -1;
	    RETURN;
	END_IF;
	// Distribution de l'ordre de positionnement pour tout les mobiles du circuit
	FOR #i := 0 TO "Nbr_Mob_Circuit" DO
	    //Test fin circuit
	    IF #Circuit.Mob_Circuit[#i].Mobile = -1 THEN
	        EXIT;
	    END_IF;
	    #Ret_Dem_Mobile := "Dem_Mobile"("Index_Mobile" := #Circuit.Mob_Circuit[#i].Mobile);
	    #Ret_Reserv_Mobile := "Reserv_Mobile"(Index_Mobile := #Circuit.Mob_Circuit[#i].Mobile,
	                                          Num_Circuit := #Circuit.Num_Circuit,
	                                          Num_Lot := #Circuit.Num_Lot);
	    // Test du Ret_Dem_Mobile
	    IF #Ret_Dem_Mobile = -1 THEN
	        #Dem_Circuit := -1;
	        RETURN;
	    END_IF;
	END_FOR;
	#Dem_Circuit := 1;
	RETURN;
END_FUNCTION

