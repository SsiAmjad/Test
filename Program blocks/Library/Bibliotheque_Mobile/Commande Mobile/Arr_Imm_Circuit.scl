FUNCTION "Arr_Imm_Circuit" : SInt
TITLE = Arret Immediat  circuit
{ S7_Optimized_Access := 'TRUE' }
AUTHOR : 'I.Daoudi'
FAMILY : Mobile
VERSION : 1.0
//Arret Immediat  circuit
   VAR_INPUT 
      Circuit : "S_Circuit";
   END_VAR

   VAR_TEMP 
      i : USInt;
      Ret_Test_Valid_Index : SInt;
      Ret_Arr_Imm_Mobile : SInt;
   END_VAR


BEGIN
	// +---------+------------+-------------+------------------------------------------------------------------------+
	// | Version | Date       | Autheur     | Ajouts/Modifications                                                   |
	// +---------+------------+-------------+------------------------------------------------------------------------+
	// | V_1     | 15.08.2023 | I.DAOUDI    | Creation du block                                                      |
	// +---------+------------+-------------+------------------------------------------------------------------------+
	// Test du Ret_Test_Valid_Index
	IF NOT "Valid_Index"(#Circuit.Mob_Circuit[0].Mobile) THEN
	    #Arr_Imm_Circuit := -1;
	    RETURN;
	END_IF;
	// Distribution de l'ordre d'arret immediat pour tout les mobiles du circuit
	FOR #i := 0 TO "Nbr_Mob_Circuit" DO
	        //Test fin circuit
	    IF #Circuit.Mob_Circuit[#i].Mobile = -1 THEN
	        EXIT;
	    END_IF;
	    #Ret_Arr_Imm_Mobile := "Arr_Imm_Mobile"("Index_Mobile" := #Circuit.Mob_Circuit[#i].Mobile);
	        // Test du Ret_Dem_Mobile
	    IF #Ret_Arr_Imm_Mobile = -1 THEN
	        #Arr_Imm_Circuit := -1;
	        RETURN;
	    END_IF;
	END_FOR;
	#Arr_Imm_Circuit := 1;
	RETURN;
END_FUNCTION

